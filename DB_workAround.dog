/- Work around for V1 of ProteusDB

struct GLOBAL{
    me string: stripString(me string: s) <- {
        if(s=="") {return("")}
        while(s[s.size()-1]==" "){s <- s.subStr(0, s.size()-1)}
        while(s[0]==" "){s <- s.subStr(1, s.size()-1)}
        return(s)
    }

    void: split(me string: s, me string: divider, me string[their list]: strList) <- {
        me int: strLen <- s.size()
        me int: divLen <- divider.size()
        me string: segment
        withEach pos in RANGE(0..strLen){
            if(s.subStr(pos, divLen)==divider){
                strList.pushLast(segment)
                segment <- ""
                pos <- pos + (divLen-1)
            } else {segment <- segment + s[pos]}
        }
        segment <- stripString(segment)
        if(segment != ""){strList.pushLast(segment)}
    }

    me bool: isNumberSegment(me string: seg, their int64: result) <- {
        if(seg[0]=="T" and seg.size()>2 and seg[1]=="_"){
            me int: strSize <- seg.size()
            me int64: acc <- 0
            withEach pos in RANGE(2..strSize){
                me char: ch <- seg[pos]
                if(isdigit(ch)){
                    acc <- (acc*10)+(ch-48)
                } else {return(false)}
            }
            result <deep- acc
            log("For "+seg+" returning:"+toString(acc))
            return(true)
        }
        return(false)
    }

    me bool: isNumberRange(me string: seg, their int64: earliest, their int64: latest) <- {
        if(seg[0]=="R" and seg.size()>2 and seg[1]=="_"){
            me int: strSize <- seg.size()
            me int64: EarliestAcc <- 0
            me int64: LatestAcc <- 0
            me int: crntSegment <- 1
            withEach pos in RANGE(2..strSize){
                me char: ch <- seg[pos]
                if(isdigit(ch)){
                    if(crntSegment==1){
                        EarliestAcc <- (EarliestAcc*10)+(ch-48)
                    } else {
                        LatestAcc <- (LatestAcc*10)+(ch-48)
                    }
                } else if(ch=="_"){
                    crntSegment <- crntSegment + 1
                } else {return(false)}
            }
            if(crntSegment != 2){return(false)}
            earliest <deep- EarliestAcc
            latest <deep- LatestAcc
            return(true)
        }
        return(false)
    }

    me string: ProteusQuery(me string: protText) <- {
        me string: result
        if (protText==""){return("")}
        else if (protText[0]=="%"){
            me string[list]: withEquals
            me string[list]: nameSegs
            split(protText, "=", withEquals)
            split(withEquals[0], ".", nameSegs)
            our infon: infLevel
            me bool: itemWasFound <- false
            withEach seg in nameSegs{
                if(seg=="%W"){infLevel <- CORE.WORLD}
                else{
                    if(infLevel!=NULL){
                        if(infLevel.value.fType==LST){
                            me bool: segFound <- false
                            me int64: segSlotPos
                            me int64: earliest
                            me int64: latest
                            if(isNumberSegment(seg, segSlotPos)){
                                our infon[itr multimap int64]: T_item <- infLevel.value.T_index.find(segSlotPos)
                                if(T_item != infLevel.value.T_index.end()){
                                    infLevel <- T_item.val
                                    segFound <- true
                                } else { /- if it wasn't found but there is a '=', add it.
                                    if(withEquals.size()>1 and seg_key == (nameSegs.size()-1)){
                                        our infon: newItem <- CORE.parseAndExtract(withEquals[1])
                                        infLevel.value.T_index.insert(segSlotPos, newItem)
                                        infLevel <- newItem
                                        segFound <- true
                                        withEquals.clear()
                                    }
                                }
                            } if(isNumberRange(seg, earliest, latest)){
                                /-log("For "+seg+" returning:"+toString(earliest) + " - " + toString(latest))
                            } else {
                                withEach infItem in infLevel.value.items{
                                    if(infItem.type!=NULL and infItem.type.asGiven==seg){
                                        infLevel <- infItem
                                        segFound <- true
                                        break()
                                    }
                                }
                            }
                            if(segFound){itemWasFound <- true; log("INFON FOUND:"+CORE.a.printInfon(infLevel))}
                            else{
                                itemWasFound <- false
                                log("Segment not found:"+seg)
                                break();
                            }
                        }
                    }
                }
            }
            if(itemWasFound and withEquals.size()>1){
                our infon: newRHS <- CORE.parseAndExtract(withEquals[1])
                if(newRHS!=NULL){
                    our infItr:: ii
                    ii.item <- newRHS
                    infLevel.wrkList.pushLast(ii)
                    infLevel.isNormed <- false
                    infLevel.wrkItmsAdded <- false
                    log("Normalizing:"+CORE.a.printInfon(infLevel))
                    CORE.a.normalize(infLevel)
                    log("Normalized:"+CORE.a.printInfon(infLevel))
                    result <- CORE.a.printInfon(infLevel) + "   SIZE:" + CORE.a.printPureInfon(infLevel.size(), NULL)
                } else {
                    result <- "Parse Error:" + CORE.parser.errorMesg + " at line " + toString(CORE.parser.errLineNum) + ":" + toString(CORE.parser.errCharPos)
                }
            } else {
                result <- CORE.a.printInfon(infLevel) + "   SIZE:" + CORE.a.printPureInfon(infLevel.size(), NULL)
            }
        } else {
            our infon: crntInfon <- CORE.parseAndExtract(protText)
            if(crntInfon!=NULL){
                CORE.a.appendToWorld(crntInfon)
                CORE.a.normalize(crntInfon)
                result <- CORE.a.printInfon(crntInfon) /- + "   SIZE:" + CORE.a.printPureInfon(crntInfon.size(), NULL)
            } else {
                result <- "Parse Error:" + CORE.parser.errorMesg + " at line " + toString(CORE.parser.errLineNum) + ":" + toString(CORE.parser.errCharPos)
            }
        }
        return(result)
    }

    void: loadSessionFile(me string: filename) <- {
        me sessionFile: session
        session.openToRead("sessionLog.pr")
        me string: fLine
        while(! session.eof()){
            fLine <- session.readEntry()
            print("   fLINE:", fLine, "\n")
            ProteusQuery(fLine)
        }
        session.closeReading()

    }

} /- END GLOBAL
