// ModelManager.dog

struct GLOBAL{
    const int: tvFalse    <- 0
    const int: tvTrue     <- 1
    const int: tvUnknown  <- 2
}
//struct partOfSpeech: inherits=<mode[posUnknown, posNone, posFuncWord, posNoun, posVerb, posAdj, posAdv, posNegotator, posName, posNum]> {}

model WordDefn{
    me string: defWord
    me string: modelID
    me List<me string>: UsageTags
    our infon: meaning
    me string: langSpecTag      // e.g., 'eng:us:southern::slang'
    mode[posUnknown, posNone, posFuncWord, posNoun, posVerb, posAdj, posAdv, posNegotator, posName, posNum]: defPOS     // The primary use for this word in speech
    me bool: tagExists(me string: tag) <- {
        withEach usageTag in UsageTags{
            if(usageTag==tag){return(true)}
        }
        return(false)
    }
    void: clear() <- {defWord<-""; modelID<-""; UsageTags.clear()}
}
struct WordDefn{}

model WordUse{
    me string: asGiven
    me string: modelID
    our WordDefn: baseDefinition
    our infon: defToUse
    mode[mpNotDone, normIsBase, N2Plural, N2Genative, N2PluralGenative, V2S_form, V2ed, V2ing,
         adj2er, adj2est, edv2er, adv2est, adj2adv_ly, wordFromAffixes, compoundWord, mpOther]: mapFromBase
    mode[posUnknown, posNone, posFuncWord, posNoun, posVerb, posAdj, posAdv, posNegotator, posName, posNum]: usePOS

    mode[wmPreFetch, wmFetchModels, wmTryHarder, wmAssembleAlt, wmDONE]: nextStage
    mode[vfNone, vfBase, vfPastSimple_ed1, vfPastParticiple_ed2, vfPresentParticiple_ing, vf3rdPersonPresentSimple_s]: verbForm
    mode[fConjunction, fPreposition, fAuxVerb, fPronoun, fDeterminer, fQuantifer]: functionWordType
    mode[numWord, numOrdinal, numCardinal, numNominal, numInfonal]: numberWordType
    flag: meaningNotFound
    flag: plural
    flag: possessive
    flag: base_ING_AsGerund   // Change a verb to a noun: *Swimming* is fun. *Eating sweets* can harm your teeth

    me int: isCompatibleWith(our WordUse: wordS) <- {
        if(self==NULL){return(tvTrue)} else if(wordS==NULL){return(tvUnknown)}
        if(asGiven == "" or wordS.asGiven == ""){return(tvTrue)}
        //log("IS_COMPAT: asGiven: "+asGiven+ " defToUse:"+toString(wordS.defToUse))
        if(asGiven == wordS.asGiven){return(tvTrue)}
        if(wordS.defToUse!=NULL and wordS.defToUse.type!=NULL and wordS!==wordS.defToUse.type){
            me int: isCompat <- isCompatibleWith(wordS.defToUse.type)
            if(isCompat==tvTrue){return(tvTrue)}
        }
        if(defToUse!=NULL and defToUse.type!=NULL and self!==defToUse.type){
            me int: isCompat <- wordS.isCompatibleWith(defToUse.type)
            return(isCompat)
        }
        return(tvFalse)
    }
    void: clear() <- {asGiven<-""}
    me string: stringify() <- {
        me string: S <- ""
        if(asGiven!=''){S<+-" asGiven:"+asGiven}
        if(defToUse){S<+-" defToUse:"+toString(defToUse)}
        return(S)
    }
    none: WordUse(me string: word, our WordDefn: wordD) <- {
        asGiven        <- word
        modelID        <- wordD.modelID
        baseDefinition <- wordD
        defToUse       <- wordD.meaning
        usePOS         <- wordD.defPOS
    //me int: POS <- usePOS; log("WORD_CONSTRUCT:'"+asGiven+"' POS:"+toString(POS))
    }
}
struct WordUse{}

struct vocabularySpec{
    me string: langSpec
    me string: baseLang
    void: init(me string: languageSpec) <- {
        // TODO: add more detail as needed
        langSpec       <- unicodeMgr.normalizeStr(languageSpec)
        baseLang       <- ""
        withEach pos in RANGE(0..langSpec.size()){
            me char: ch  <- langSpec[pos]
            if(ch=="_" or ch=="-"){break()}
            baseLang <+- ch
        }
    }
}

struct GLOBAL{
    me bool: checkEnd(me string: word, me string: sufx, their int: sSze) <- {
        me int: wSize <- word.size()
        me int: sSize <- sufx.size()
        if(wSize <= sSize){return(false)}
        withEach idx in RANGE(1..sSize+1){
            if(word[wSize-idx] != sufx[sSize-idx]){
                return(false)
            }
        }
        sSze <deep- sSize
        return(true)
    }
}

struct AgentContext {
    our infon: topicHistory

    void: addContextItem(our infon: itemToAdd) <- {
        if(itemToAdd.asNot){return()}
        if(topicHistory.value.items.first() === itemToAdd){return()}
        logSeg("-add2Ctx")
        // Remove any already-existing references to itemToAdd
        our POV: pov <- topicHistory.value.items.head
        while(pov != NULL){
            if(pov.dpItem() === itemToAdd){
                pov <- topicHistory.value.items.erase(pov)
            } else {pov <- pov.next}
        }

        topicHistory.value.items.prepend(itemToAdd)
        //log("  ADD_TO_CTX: added:"+itemToAdd.mySymbol()+": "+toString(itemToAdd)+"  size:"+toString(topicHistory.value.items.size()))
        if(itemToAdd.proxy!=NULL){log("       PROXY:"+itemToAdd.proxy.mySymbol())}
        cull(10)
    }
    our infon: searchFor() <- {
        return(NULL)
    }
    void: cull(me int: maxItems) <- {
        while(topicHistory.value.items.size()>maxItems){
            topicHistory.value.items.popLast()
        }
    }

    void: init() <- {
        Allocate(topicHistory)
        topicHistory.setToList()
        topicHistory.value.tailUnfinished <- false
        topicHistory.dontProcess <- true
    }
}
struct ModelManager {
    me Multimap<me string, our WordDefn>: wordToModel
    // TODO: me Multimap<me string, our infon>: wordsWaitingForDefinition

    void: indexDefinition(their Agent: agent, our WordDefn: wordD, our infon: infModel) <- {
        // Set Meaning
        wordD.meaning <- infModel
        //Normalize and separate the word and modelID
        wordD.defWord <- unicodeMgr.normalizeStr(wordD.defWord)
        me int: underScorePos <- findString(wordD.defWord, "_")
        if(underScorePos!=-1){
            wordD.modelID <- wordD.defWord.subStr(underScorePos+1, wordD.defWord.size())
            wordD.defWord <- wordD.defWord.subStr(0, underScorePos)
        }
        // Set the locale
        if(!wordD.UsageTags.isEmpty()){
            wordD.langSpecTag <- unicodeMgr.normalizeStr(wordD.UsageTags[0])
        }else{wordD.langSpecTag <- unicodeMgr.normalizeStr(agent.getLocaleBaseName())}

        withEach tag in wordD.UsageTags{
            if(tag=="det" or tag=="quantfr" or tag=="quantfr" or tag=="ProN" or tag=="verbHlpr" or tag=="prep" or tag=="conj"){wordD.defPOS <- posFuncWord}
            else if(tag=="noun"){wordD.defPOS <- posNoun; log("SET_AS_NOUN")}
            else if(tag=="verb"){wordD.defPOS <- posVerb}
            else if(tag=="adj") {wordD.defPOS <- posAdj}
            else if(tag=="adv") {wordD.defPOS <- posAdv}
            else if(tag=="nego"){wordD.defPOS <- posNegotator}
            else if(tag=="name"){wordD.defPOS <- posName}
            else if(tag=="num"){wordD.defPOS  <- posNum}
            else{wordD.defPOS  <- posNone}
        }
        // Store to multimap
        //log("STORE_DEF: '"+wordD.defWord+"'")
        wordToModel.insert(wordD.defWord, wordD)
    }

    //me ModelID: findModelIDFromInfon(our infon: inf) <- {}
    //our infon: findModelFromModelID(me ModelID: modelID) <- {}
    //our wordList: findWordsForMeaningID(me ModelID: modelID, their vocabularySpec: vSpec) <- {}

    me bool: wordValidForLang(me string: lSpec, their vocabularySpec: vSpec) <- {
        if(lSpec=="" or lSpec==vSpec.baseLang or lSpec==vSpec.langSpec){return(true)}
        return(false)
    }

    me int: lookupWord(me string: word, their vocabularySpec: vSpec, their List<our WordDefn>:results) <- {
        me int: count <- 0
        withEach wordDef in wordToModel from word to word{
            //log("DE_REF_WORD:"+word+"  -->  "+wordDef.defWord+"_"+wordDef.modelID)
            if(wordValidForLang(wordDef.langSpecTag, vSpec)){
                results.append(wordDef)
                count <+- 1
            }
        }
        return(count)
    }

    me int: FetchBasePlusPOS_forms_stage1(me string: word, their vocabularySpec: vSpec, their List<our WordUse>:results) <- {
        // TODO: Reversably tablize this
        me int: numDefsBefore <- results.size()
        me List<our WordDefn>: resultDefns
        me int: numAdded <- lookupWord(word, vSpec, resultDefns)
        withEach idx in RANGE(numDefsBefore..numDefsBefore+numAdded){
            our WordUse:: newWordUse{word, resultDefns[idx]}
            newWordUse.mapFromBase    <- normIsBase
            results.append(newWordUse)
        }
        if(numAdded==1 and resultDefns[0].defPOS==posFuncWord){
            return(numAdded)
        }
        me string: wrd <- word
        me int: wLen <- wrd.size()
        me int: sSze <- 0
        me string: baseTry
        numDefsBefore <- resultDefns.size()
        if(checkEnd(wrd, "s'", sSze)){
            me string: preSufx <- wrd.subStr(0, wLen-sSze)
            numAdded <- lookupWord(preSufx, vSpec, resultDefns)
            withEach idx in RANGE(numDefsBefore..numDefsBefore+numAdded){
                if(resultDefns[idx].defPOS==posNoun){
                    our WordUse:: newWordUse{word, resultDefns[idx]}
                    newWordUse.mapFromBase    <- N2PluralGenative
                    newWordUse.possessive     <- true
                    newWordUse.plural         <- true
                        Allocate(newWordUse.defToUse)
                        newWordUse.defToUse.setSelfAsAPluralOf(resultDefns[idx].meaning)
                    results.append(newWordUse)
                }
            }
        } else if(checkEnd(wrd, "'s", sSze)){
            me string: preSufx <- wrd.subStr(0, wLen-sSze)
            numAdded <- lookupWord(preSufx, vSpec, resultDefns)
            withEach idx in RANGE(numDefsBefore..numDefsBefore+numAdded){
                if(resultDefns[idx].defPOS==posNoun){
                    our WordUse:: newWordUse{word, resultDefns[idx]}
                    newWordUse.mapFromBase    <- N2Genative
                    newWordUse.possessive     <- true
                    results.append(newWordUse)
                }
            }
        } else if(checkEnd(wrd, "s", sSze)){
            me string: preSufx <- wrd.subStr(0, wLen-sSze)
            //log("Plural_TRY:"+preSufx + "  size:"+toString(sSze))
            numAdded <- lookupWord(preSufx, vSpec, resultDefns)
            withEach idx in RANGE(numDefsBefore..numDefsBefore+numAdded){
                if(resultDefns[idx].defPOS==posNoun){
                    our WordUse:: newWordUse{word, resultDefns[idx]}
                    newWordUse.mapFromBase    <- N2Plural
                    newWordUse.plural         <- true
                        Allocate(newWordUse.defToUse)
                        newWordUse.defToUse.setSelfAsAPluralOf(resultDefns[idx].meaning)
                    results.append(newWordUse)
                }else if(resultDefns[idx].defPOS==posVerb){
                    our WordUse:: newWordUse{word, resultDefns[idx]}
                    newWordUse.mapFromBase    <- V2S_form
                    newWordUse.verbForm       <- vf3rdPersonPresentSimple_s
                    results.append(newWordUse)
                }
            }

        } else if(checkEnd(wrd, "ed", sSze) or checkEnd(wrd, "ing", sSze) or checkEnd(wrd, "er", sSze) or checkEnd(wrd, "est", sSze)){
            me string: preSufx <- wrd.subStr(0, wLen-sSze)
            me int: preSSze <- preSufx.size()
            if(preSSze >= 2){
                me string: preSfx <- preSufx.subStr(preSSze-2, 2)
                if(preSfx=="ll"){
                    baseTry <- preSfx.subStr(0, preSSze-1)  // eng:us
                    baseTry <- preSfx                       // eng:uk
                } else if(preSfx=="ng" or preSfx=="el" or preSfx=="al" or preSfx=="ol"){
                    baseTry <- preSfx
            //    } else if(isConsonant(preSfx[0]) and preSfx[0]==preSfx[1]){
            //        baseTry <- preSfx.subStr(0, preSSze-1)
                } else{
                    baseTry <- preSfx
                }
            }
        } else if(checkEnd(wrd, "ly", sSze)){
            me string: preSufx <- wrd.subStr(0, wLen-sSze)
            numAdded <- lookupWord(preSufx, vSpec, resultDefns)
            withEach idx in RANGE(numDefsBefore..numDefsBefore+numAdded){
                if(resultDefns[idx].defPOS==posAdj){
                    our WordUse:: newWordUse{word, resultDefns[idx]}
                    newWordUse.mapFromBase    <- adj2adv_ly
                    newWordUse.usePOS         <- posAdv
                    results.append(newWordUse)
                }
            }
        }
        return(results.size())
    }

    me int: FetchBasePlusPOS_forms_stage2(me string: word, their vocabularySpec: vSpec, their List<our WordUse>:results) <- {
        // Try any items missed or not chosen yet. Try making words from context including from prefixes and suffixes.
        return(0)
    }


    our infon: constructAltModelFromWord(their Agent: agent, our WordUse: wordS, their vocabularySpec: vSpec, me string: scopeID) <- {
        if(wordS.nextStage==wmPreFetch){
            // Normalize and separate the word and modelID
            wordS.asGiven <- unicodeMgr.normalizeStr(wordS.asGiven)
            me int: underScorePos <- findString(wordS.asGiven, "_")
            if(underScorePos!=-1){
                wordS.modelID <- wordS.asGiven.subStr(underScorePos+1, wordS.asGiven.size())
                wordS.asGiven <- wordS.asGiven.subStr(0, underScorePos)
            }
            // TODO: As optimization, if modelID is given, look it up and verify that a word derivable from asGiven matches. Then skip stage 1.

            wordS.nextStage <- wmFetchModels
        }
        me List<our WordUse>: results
        if(wordS.nextStage==wmFetchModels){
            me int: numAlts <- FetchBasePlusPOS_forms_stage1(wordS.asGiven, vSpec, results)
            if(numAlts==0){
                numAlts <- FetchBasePlusPOS_forms_stage2(wordS.asGiven, vSpec, results)
            }
            if(numAlts==0){
                wordS.defToUse <- NULL
                wordS.meaningNotFound <- true
                wordS.nextStage <- wmDONE
            }else if(numAlts==1){
                Allocate(wordS.defToUse)
                results[0].defToUse.copyAsTypeTo(wordS.defToUse, true)
                wordS.flags <- results[0].flags
                wordS.nextStage <- wmDONE
            }else if(numAlts>1){
                wordS.nextStage <- wmAssembleAlt
            }
        }
        if(wordS.nextStage==wmAssembleAlt){
            // sortResultsByScore(results, scopeID)   //: TODO finish this
            //
            our infon: newInnr <- makeInnrInfon()
            withEach infMatch in results{
                our infon:: defToUse
                infMatch.defToUse.copyAsTypeTo(defToUse, true)
                newInnr.value.items.append(defToUse, false)
            }
            our infon:: newAltInfon
            newAltInfon.convertThisToIntersection(newInnr, fromGiven) // Make [ ] and add innr to it
            wordS.defToUse <- newAltInfon
            wordS.nextStage <- wmDONE
        }
        if(wordS.nextStage==wmDONE){
            return(wordS.defToUse)
        }
        return(NULL)
    }

    void: clear()<-{wordToModel.clear()}
}
