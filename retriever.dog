/- retriever.dog

model aItem{
    me int:           priority
    our infDeepItr:   LHS
    our infDeepItr:   RHS
    our aItem[list]:  predecessors
    our aItem:        successor
    me flag:          copyRHSTypeToLHS
    me flag:          rejected
    me flag:          looseMode
    me flag:          stitched
    me mode[valueFailed, valueVerified, copyValueLHStoRHS, copyValueRHStoLHS]:        valueAction
    me mode[sizeFailed, sizeVerified, copySizeLHStoRHS, copySizeRHStoLHS]:        sizeAction
    me uint64: sizeToCopy

    me mode[getAllPlusSize, startPos, endPos, query, size, length, first, last, Nth, tagged, typed]:opTypes

    me void: init(me int: Priority, our infDeepItr: lhs, our infDeepItr: rhs)<-{
        priority <- Priority
        Allocate(LHS)
        lhs.copyThisTo(LHS)
        if(rhs==NULL){RHS<-NULL}
        else{
            Allocate(RHS)
            rhs.copyThisTo(RHS)
        }
    }

    void: addRHS(our infDeepItr: IDP) <- {
        if(RHS==NULL){
            Allocate(RHS)
            IDP.copyThisTo(RHS)
        } else {
            our infDeepItr: crntRHS <- RHS
            while(crntRHS.nextAnd!=NULL){
                crntRHS <- crntRHS.nextAnd
            }
            Allocate(crntRHS.nextAnd)
            IDP.copyThisTo(crntRHS.nextAnd)
        }
    }

    me string: printAgendaItem()<-{
        if(self == NULL){return("NULL")}
        me agent: a
        me string: itemStr <- "["
        me string: innrStr
        itemStr <- itemStr + toString(priority) + ": "
        if (LHS.crntCursor !=NULL){
            innrStr <- a.printInfon(LHS.crntCursor.item)
            if(LHS.state==sSecondVisit) {innrStr <- "<"+innrStr + ">"}
            itemStr <- itemStr + innrStr;
        }
        else{itemStr <- itemStr +" ITEM-NULL"}
        if(looseMode!=0){itemStr <- itemStr + " |==| "}
        else{itemStr <- itemStr + " |=| "}
        if (RHS!=NULL and RHS.crntCursor!=NULL){
            innrStr <- a.printInfon(RHS.crntCursor.item)
            if(RHS.state==sSecondVisit) {innrStr <- "<"+innrStr + ">"}
            itemStr <- itemStr + innrStr;
        }else{itemStr <- itemStr +"ITEM-NULL"}

        me string: predStr <- " | "
        withEach pred in predecessors{
            predStr <- predStr + pred.mySymbol(pred) + " "
        }

        itemStr <- itemStr + predStr + "]"
        return(itemStr)
    }

}

struct priorityQ{
    our aItem[multimap int]: PQ
    our aItem[multimap int]: usedItems

    me void: push(our aItem: newItem) <- {
        PQ.insert(newItem.priority, newItem)
    }

    our aItem: pop() <- {
        our aItem: min <- PQ.front()
        usedItems.insert(min.priority, min)
        PQ.popFirst()
        return(min)
    }

    me bool: isDuplicate(our aItem: agendaItem) <- {
        withEach item in PQ from agendaItem.priority to agendaItem.priority {
            if((agendaItem.LHS.crntCursor===item.LHS.crntCursor) and (agendaItem.RHS.crntCursor===item.RHS.crntCursor)){return(true)}
            /- if(agendaItem.LHS.isDuplicateOf(item.LHS) and agendaItem.RHS.isDuplicateOf(item.RHS)){return(true)}
        }

        withEach item in usedItems from agendaItem.priority to agendaItem.priority {
            if((agendaItem.LHS.crntCursor===item.LHS.crntCursor) and (agendaItem.RHS.crntCursor===item.RHS.crntCursor)){return(true)}
            /- if(agendaItem.LHS.isDuplicateOf(item.LHS) and agendaItem.RHS.isDuplicateOf(item.RHS)){return(true)}
        }
        return(false)
    }

    me bool: isEmpty() <- {
        return(PQ.size() == 0)
    }
}

struct retriever{
    me priorityQ: agenda
    me int: errorCode
    me string: errorMsg
    their agent: parentAgent

    void: addItem(our aItem: agendaItem)<-{
        if(!agenda.isDuplicate(agendaItem)){
            log("ADDING WORK-ITEM:" + agendaItem.mySymbol(agendaItem) + ": \t" + agendaItem.printAgendaItem() + "##########################")
    if(agendaItem.LHS.fromDotted and agendaItem.RHS.fromDotted){
        log("@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@" +  agendaItem.printAgendaItem() )
    /*    our infon: LHS <- agendaItem.LHS.crntCursor.item
        LHS.infMode <- isLiteral
        LHS.value.fType <- LST
        LHS.itmMode <- mDots
        LHS.value.format <- fUnknown
        LHS.value.cursor <- cDefault

        our infon: RHS <- agendaItem.RHS.crntCursor.item
        RHS.infMode <- isLiteral
        RHS.value.fType <- LST
        RHS.itmMode <- mDots
        RHS.value.format <- fUnknown
        RHS.value.cursor <- cDefault*/
        agendaItem.LHS.preserveDots <- true
    }
            agenda.push(agendaItem)
      /-      TopAItemDash.updatePush(agendaItem)
        }
    else {log("DUPLICATE WORK-ITEM:" + agendaItem.printAgendaItem() + "##########################")}
    }

    void: printAgenda()<-{
        print("    |PRINT AGENDA____________\n")
        withEach agendaItem in agenda.PQ {
            print("    |        item: ", agendaItem.printAgendaItem(), "\n")
        }
        print("    |______________________\n")
    }

    our aItem: resolve(our aItem: agendaItem, me string: indent)<-{
        our aItem: retVal <- NULL
        if(agendaItem.predecessors.size()>0){
            /- Choose pedigree
            me int: pedigreeChosen <- agendaItem.predecessors.size()-1 /- 0 /- TODO: if chosen has RHS=NULL, choose another if there is one.
            our aItem: pred <- agendaItem.predecessors[pedigreeChosen]
            pred.successor <- agendaItem
            retVal <- resolve(pred, indent +"    ")
            if(retVal==NULL){retVal<-pred}
        }

        return(retVal)
    }

    void: processAgenda()<-{
        withEach count in WHILE(!agenda.isEmpty()) {
            if (count > 100){break()}  /-TODO: remove this when safe
            processAgendaItem(agenda.pop())
        }
    }

    void: addItemsToAgenda(our aItem: pred, me bool: looseMode, our infDeepItr: LHSChain, our infDeepItr: RHSChain, me bool: breakDotCycle)<-{
        me int: priority <- pred.priority
        if(pred.LHS.crntCursor.item.value.fType!=LST and (pred.RHS==NULL or pred.RHS.crntCursor==NULL or pred.RHS.crntCursor.item.value.fType!=LST)){priority <- priority+1}
        our infDeepItr: currentLHS <- LHSChain
        while(currentLHS != NULL){
            if(currentLHS.state != sEOL){
                parentAgent.prepWorkList(currentLHS)
                me int: numItemsAdded <- parentAgent.addWorkItemsToAgenda(priority, pred, currentLHS)
                our infDeepItr: currentRHS <- RHSChain
                while(currentRHS != NULL) {
                    if(numItemsAdded==0 or currentRHS.crntCursor!=NULL){
                        our aItem: nextItem
                        Allocate(nextItem)
                        nextItem.init(priority, currentLHS, currentRHS)
                        nextItem.looseMode <- looseMode
                        if(pred!=NULL){nextItem.predecessors.pushLast(pred)}
                        if(!breakDotCycle or (!nextItem.LHS.fromDotted and !nextItem.RHS.fromDotted))
                            {addItem(nextItem)}
                    }
                    currentRHS <- currentRHS.nextOr
                }
            }
            currentLHS <- currentLHS.nextOr
        }
        /-printAgenda()
    }

    me bool: readyToResolve(our aItem: agendaItem)<-{
        me bool: retVal <- false

        our infDeepItr: LHS <- agendaItem.LHS
        our infDeepItr: RHS <- agendaItem.RHS
       /- print("    CONDITIONS: ", " |RHS.nestLvl: ", RHS.nestLvl, " |RHS.state: ", RHS.state, " |LHS.nestLvl: ", LHS.nestLvl, "\n")

        if(LHS.nestLvl == 0 and RHS.nestLvl == 0 and LHS.state == sSecondVisit){retVal <- true}
        else if(LHS.nestLvl == 0 and RHS.nestLvl == 0 and RHS.state == sEOL and !RHS.isAList){retVal <- true}

        if (retVal and !agenda.isEmpty()){retVal <- false}

        return(retVal)
    }

    void: processAgendaItem(our aItem: agendaItem)<-{
        log("processAgendaItem: " + agendaItem.printAgendaItem())
        me bool: breakDotCycle <- false
        our infDeepItr: LHS <- agendaItem.LHS
        our infItr: crntCursor <- LHS.crntCursor
        me int: agendaLHS_state <- agendaItem.LHS.state
        me bool: RHS_Empty <- agendaItem.RHS==NULL or agendaItem.RHS.crntCursor==NULL
        me int: numItemsAdded <- 0
        our infDeepItr: remainder <- NULL
        me bool: looseMode <- agendaItem.looseMode
        if(agendaLHS_state == sNotASeq or agendaLHS_state == sFirstVisit){
            if(!RHS_Empty){
                if(agendaItem.RHS.state != sSecondVisit){
                    log("processAgendaItem - ! RHS.sSecondVisit")
                    remainder <- parentAgent.validateAndAnnotate(agendaItem)
                    if(agendaItem.rejected){log("processAgendaItem - REJECT"); return();}
                } else {
                    log("processAgendaItem - RHS.SecondVisit!")
                    if(agendaItem.RHS.crntCursor.item.itmMode!=mSubItems){ /- TODO: Or if parent is in concat
                        return()
                    }
                }
            }
            if(LHS.nestLvl == 0 and agendaLHS_state == sNotASeq){   /- Stitching
                parentAgent.stitch(agendaItem, "")
            }
        } else if(agendaLHS_state == sSecondVisit){
            log("processAgendaItem - LHS.SecondVisit")
            if(readyToResolve(agendaItem)){
                our aItem: topItem <- resolve(agendaItem, "    resolve: ")
                parentAgent.stitch(topItem, "")
            }
        } else {print("IMPOSSIBLE LHS-STATE:", agendaLHS_state); exit(2)}

        if(agendaItem.LHS.fromDotted and agendaItem.RHS.fromDotted){breakDotCycle<-true}
        our infDeepItr: LHSChain <- NULL
        our infDeepItr: RHSChain <- NULL

        if(RHS_Empty){
            LHSChain <- agendaItem.LHS.getNextDeDotted()
            Allocate(RHSChain);  RHSChain.crntCursor <- NULL
        } else {
            me bool: remainderSideRHS <- true  /- Later this may be false if the LHS has a remainder
            if(remainder != NULL){
                    log("processAgendaItem - RHS-1")
                if(remainderSideRHS){
                    RHSChain <-remainder
                } else {
                    LHSChain <-remainder
                }
            }

            me int: LHSfType <- agendaItem.LHS.crntCursor.item.value.fType
            me int: RHSfType <- 0; if(!RHS_Empty){RHSfType <- agendaItem.RHS.crntCursor.item.value.fType}
            if (LHSChain == NULL){
                if (LHSfType != LST and RHSfType == LST){
                    LHSChain <- agendaItem.LHS
                }else {
                    LHSChain <- agendaItem.LHS.getNextDeDotted()
                }
            }
            if (RHSChain == NULL){
                log("processAgendaItem - RHS-2a")
                if (LHSfType == LST and RHSfType != LST){
                    RHSChain <- agendaItem.RHS
                }else{
                    RHSChain <- agendaItem.RHS.getNextDeDotted()
                  /-  if(RHSChain.state==sEOL){print("<EOL> "); return()} /- Return here makes testItem/test1 fail
                }
            }
        }
        addItemsToAgenda(agendaItem, looseMode, LHSChain, RHSChain, breakDotCycle)

/- VIEWER    dataManager.styler.crntLHS <- NULL;  dataManager.styler.crntRHS <- NULL; /- Reset colors for dashboard
    }

}

do codeDataDisplay(aItem, TAG_infonDumpMode)
