
// Infon List

model POV{
    our infon: item
    our POV: next
    our POV: prev
    our POV: altPOV
    our POV: parent
    flag: looseMode
    flag: looseTop
    flag: looseInhtd
    flag: wasRejected
    flag: altWasTried
    flag: readyToResolve
    flag: addNewSubDots
    me int: AlternativesNestLvl
    our POV[map string]: pctTags
    our POV[list]: predecessors
    me int: linearAltID
    mode[cmdNoOp, delDots, keepDots, insB4Dots, insAftDots]: stitchCmd

    me bool: isFirst() <- {return(prev==NULL)}
    me bool: isLast()  <- {return(next==NULL)}
    me bool: looseSize() <- {
        return(looseInhtd!=false or looseTop!=false)
    }
    void: insertNewPctTag(me string: tag, our POV: newPOV) <- {
        /*our POV[itr map string]: tagPOV <- pctTags.find(tag)
        if(tagPOV == pctTags.end()){
            pctTags.insert(tag, newPOV)
        }else{
            tagPOV.val.item.addInfItrToWrkList(newPOV)
        }*/
    }
    void: closeItemsAtPOV(our POV: lastItem) <- {
        while(!item.value.items.isEmpty() and !(item.value.items.last() === lastItem.item)){
            item.value.items.popLast()
        }
        withEach itm in item.value.items{
            itm.isTentative <- false
        }
        if(stitchCmd != keepDots){
            item.value.tailUnfinished <- false
            item.countSize()
        }
    }
    our POV: findOrAddPctTag(me string: tag) <- {
        our POV: retVal
        our POV[itr map string]: tagPOV <- pctTags.find(tag)
        if(tagPOV == pctTags.end()){
            Allocate(retVal)
            Allocate(retVal.item)
            pctTags.insert(tag, retVal)
        }else{
            retVal <- tagPOV.val
        }
        return(retVal)
    }
    our POV: tryAddingTentativeChild() <- {
        me string: lenStatus <- item.lengthStatus()
        if(lenStatus == "L" or lenStatus == "?"){
            our infon:: newInfon
            our infon: infonType <- item.value.listSpec
            if(infonType != NULL){
                newInfon <deep- infonType // MUST: be a lazy cow copy
            }else{
                newInfon.value.fType   <- tUnknown
                newInfon.infMode       <- isUnknown
                newInfon.itmMode       <- mItem
                newInfon.value.format  <- fUnknown
            }
            if(lenStatus == "?"){newInfon.isTentative <- true}
            our POV: newInfNode <- item.value.items.pushLast(newInfon)
            return(newInfNode)
        }else if(lenStatus == "E"){
            item.value.tailUnfinished <- false
            return(NULL)
        }else if(lenStatus == "G"){log("INFON TOO BIG")}
        return(NULL)
    }
    our POV: getFirst(me bool: skipDots) <- { // WARNING: item must be a literal List.
        our POV: firstNode
        if(item.value.items.isEmpty()){
            if(!skipDots and item.value.tailUnfinished){
                firstNode <- tryAddingTentativeChild()
                if(firstNode == NULL){return(NULL)}
            }
            else{return(NULL)}
        }
        else{
            firstNode <- item.value.items.begin()
        }
        return(firstNode)
    }
    our POV: getNext(me bool: skipDots) <- {
        while(true){
            if(next==NULL){
                if(!skipDots){
                    if(parent.item != NULL and parent.item.value.tailUnfinished){
                        our POV: newChild <- parent.tryAddingTentativeChild()
                        return(newChild)
                    }
                }
		return(NULL)
            }
            if(item.itmMode == mTagDefn){
                registerClass(item)
            }else{return(next)}
        }
    }
    our POV: getFirstTight(our infItr: ii, me bool: skipDots) <- {
	our POV: nextPOV <- getFirst(skipDots)
	if(nextPOV != NULL and nextPOV.item.itmMode == mSubItems){
	    our POV: firstPOV <- nextPOV.getFirstTight(ii, skipDots)
	    if(firstPOV == NULL){nextPOV <- getNextTight(ii, skipDots)}
	    else{nextPOV <- firstPOV}
	}
	return(nextPOV)
    }
    our POV: getNextTight(our infItr: ii, me bool: skipDots) <- {
	our POV: nextPOV <- getNext(skipDots)
	if(nextPOV == NULL){
	    //if(self == ii.item){return(NULL)}
	    nextPOV <- parent.getNextTight(ii, skipDots)
	}else if(nextPOV.item.itmMode == mSubItems){
	    our POV: firstPOV <- nextPOV.getFirstTight(ii, skipDots)
	    if(firstPOV == NULL){nextPOV <- getNextTight(ii, skipDots)}
	    else{nextPOV <- firstPOV}
	}
	return(nextPOV)
    }

    me string: stringify()<-{
        me string: S
        S <+- item.mySymbol(item)+":"+stringifyInfon(item)
        if(predecessors.size() >0){
            S <+- "--PREDS--"
            withEach pred in predecessors{
                S <+- ":"+pred.mySymbol(pred)
            }
        }
        return (S)
    }
}

model DblLinkedList: implements = list {
    our POV: head
    our POV: tail

    void: clear() <- {head <- NULL; tail <- NULL;}

    our POV: pushLast(our infon: i) <- {
        our POV:: newNode {i, NULL, tail}
        if(tail!=NULL) {tail.next <- newNode}
        if(head==NULL) {head <- newNode}
        tail <- newNode
        return(newNode)
    }
    our POV: pushFirst(our infon: i) <- {
        our POV:: newNode {i, head, NULL}
        if(head!=NULL){head.prev <- newNode}
        if(tail==NULL) {tail <- newNode}
        head <- newNode
        return(newNode)
    }
    our POV: pushAsAlt(our POV: pov, our infon: i) <- {
        our POV: newNode <- pushLast(i)
        newNode.predecessors <- pov.predecessors
        newNode.altPOV <- pov
        return(newNode)
    }
    our POV: erase(our POV: toDel) <- {
        our POV: retVal <- toDel.prev
        if(toDel===head){head<-head.next}
        if(toDel===tail){tail<-tail.prev}
        if(toDel.prev){toDel.prev.next <- toDel.next}
        if(toDel.next){toDel.next.prev <- toDel.prev}
        return(retVal)
    }

    our POV: insertBefore(our POV: position, our infon: i) <- {
        our POV: prevNode <- position.prev
        our POV:: newNode {i, position, prevNode}
        if(prevNode!=NULL){prevNode.next <- newNode}
        position.prev <- newNode
        if(head === position){head <- newNode}
        return(newNode)
    }

    our POV: insertAfter(our POV: position, our infon: i) <- {
        our POV: nextNode <- position.next
        our POV:: newNode {i, nextNode, position}
        if(nextNode!=NULL){nextNode.prev <- newNode}
        position.next <- newNode
        if(tail === position){tail <- newNode}
        return(newNode)
    }

    our infon: popFirst() <- {
        our infon: retVal <- head.item
        if(head!=NULL){
            head <- head.next
            if(head==NULL){tail <- NULL}
        }
        return(retVal)
    }

    our infon: popLast() <- {
        our infon: retVal <- tail.item
        if(tail!=NULL){
            tail <- tail.prev
            if(tail==NULL){head <- NULL}
            else{tail.next <- NULL}
        }
        return(retVal)
    }

    me bool: isEmpty() <- {
        return(head==NULL)
    }

    our infon: first() <- {
        if(head) {return(head.item)}
        return(NULL)
    }

    our infon: last() <- {
        if(tail) {return(tail.item)}
        return(NULL)
    }

    our POV: begin()  <- {return(head)}
    our POV: end()    <- {return(NULL)}
    our POV: rbegin() <- {return(tail)}
    our POV: rend()   <- {return(NULL)}

    our POV: at(me int: N)  <- {
        if (head == NULL){return(NULL)}
        our POV: retVal <- begin()
        withEach x in RANGE(0..N){
            retVal <- retVal.next
            if (retVal == NULL){return(NULL)}
        }
        return(retVal)
    }

    me int: size() <- {
        me int: count <- 0
        our POV: pov <- head
        while(pov != NULL){
            count <- count+1
            pov <- pov.next
        }
        return(count)
    }
}

struct POV {}
struct DblLinkedList {}
