/-////////////////  Structures For Working with Proteus
/- Proteus.dog

LinuxTestBuild: Platform='Linux' CPU='amd64' Lang='CPP' testMode='makeTests';
/-LinuxBuild:     Platform='Linux' CPU='amd64' Lang='CPP' optimize='speed';

FileName = "Proteus"
Version = "0.8"
CopyrightMesg = "Copyright (c) 2015-2016 Bruce Long"
Authors = "Bruce Long"
Description = "Proteus Engine"
ProgramOrLibrary = "program"
TestSpec = "ProteusTests.dog"

featuresNeeded = [ ]

LicenseText = `This file is part of the "Proteus Language suite" All Rights Reserved.`
infonDumpMode='text'

do ManageCmdLine(cmd)


model pureInfon{
    me mode[tUNKNOWN, NUM, STR, LST]: fType
    me mode[fUNKNOWN, fConcat, fLiteral]: format
    me mode[fDefault, fFloat, fFraction, fSingleQuote, fHereDoc, fSizedBin]: literalFormat
    me mode[bDec, bBin, bHex]:numBase
    me mode[cDefault, cGetLast, cGetFirst, cGetMarked]: cursor
    me flag: inverted
    me flag: timelike
    flag: sizeGiven
    flag: isLstSize
    me uint64: num
    me string: str
    our infon[list]: items
    our infon: listSpec

    our pureInfon: size

    me none: pureInfon(me int: N) <- {
        fType <- NUM
        format <- fLiteral
        num <- N
    }
    me none: pureInfon(me string: S) <- {
        fType <- STR
        format <- fLiteral
        str <- S

    }
    void: deleteNth(me int: toDel) <- {items.erase(items.nthItr(toDel))}
    void: clear() <- {num<-0 str<-'' items.clear() listSpec<-NULL}
}

#define LISTBODY(LEFT_BRKT, RIGHT_BRKT) <% >{ "LEFT_BRKT" "T"[opt]:timelike  >me listInfo[opt] >our listItem[list]: items >ws "RIGHT_BRKT" } %>
model infon{
    flag: asNot
    flag: isFunc
    flag: isFuncArg
    flag: invertFunc
    mode[isUnknown, newCursor, isTag, isLiteral]: infMode
    me mode[mItem, mSubItems, mDots, mTagDefn]: itmMode
    me pureInfon: size
    me pureInfon: value
    flag: isNormed
    flag: hasSelector
    our infon: aux1
    our infon: aux2
    me string: tag
    our infItr[list]: wrkList
    /-flag: wrkListIsAlts

    our infon: getLastItem() <- {
        /- ASSERT(Should be already normed and a non-concat list)
        our infon: lastItem
        if(value.items.size()>0) {
            lastItem <- value.items[value.items.size()-1]
        }  else {return(NULL)}
        switch(lastItem.itmMode){
            case mItem:{
                return(lastItem)
            }
            case mSubItems:{return(lastItem.getLastItem())}
            case mDots:{exit(3)}
            case mTagDefn:{exit(4)}
        }
        return(lastItem)
    }
    our infon: getFirstItem() <- {
        /- ASSERT(Should be already normed and a non-concat list)
        our infon: firstItem
        if(value.items.size()>0) {
            firstItem <- value.items[0]
        }  else {return(NULL)}
        switch(firstItem.itmMode){
            case mItem:{
                return(firstItem)
            }
            case mSubItems:{return(firstItem.getFirstItem())}
            case mDots:{exit(3)}
            case mTagDefn:{exit(4)}
        }
        return(firstItem)
    }
    void: deleteNth(me int: toDel) <- {
        /- Low level delete of an item in list. Does not update size.
        if(value.fType==LST){
            value.deleteNth(toDel)
        }
    }
    void: clear() <- {size.clear() value.clear()}
    me none: infon(me string: S) <- {
        infMode <- isLiteral
        itmMode <- mItem
        size <- pureInfon(S.size())
        value <- pureInfon(S)
    }
}

model infItr{
    our infon: item
    our infItr: parent
    me  int: idx
    mode [asID, asFirst, asLast, asMarked, asFirstParent, asLastParent, asMarkedParent]: target

    our infItr: nextItr() <- {
        our infItr:: ret();
        if(parent!=NULL and item!=NULL) {
            ret.idx <- idx+1
            if(ret.idx < parent.item.value.items.size()){ret.item <- parent.item.value.items[ret.idx]}
            else {ret.item <- NULL}
        }
        ret.parent <- parent
        return(ret)
    }

    our infon: getPrevPtr() <- {
        if(parent==NULL or item==NULL or idx==0) {return(NULL)}
        return(parent.item.value.items[idx-1])
    }


    our infItr:subItemNextItr() <- {
        our infItr: nxt <- nextItr()
        withEach r in WHILE(nxt!= NULL and nxt.item == NULL and nxt.idx>1 and nxt.parent!=NULL and nxt.parent.item.itmMode==mSubItems):{
            nxt <- nxt.parent
            nxt <- nxt.nextItr()
        }
        if(nxt.item == NULL){ nxt<-NULL }
        return(nxt)
    }

    void: copyThisTo(our infItr: to) <- {
        to.item <- item
        to.parent <- parent
        to.idx <- idx
    }

    void: deleteCrnt() <- {
        parent.item.deleteNth(idx)
    }

    void: insertAtIDX(our infon: i) <- {
        parent.item.value.items.insert(parent.item.value.items.nthItr(idx), i)
    }

}

string infItr{
    "=" > ws > our infon:item
}
string listInfo[
        >{ >{"~" >ws >our pureInfon: size}[opt]:Size >ws "C" >ws >our infon:listSpec "|"[opt]}
     |  >{ >{"~" >ws >our pureInfon: size}[opt]:Size         >ws >our infon:listSpec "|"}
     |  >{ "~" >ws >our pureInfon: size >ws }
]

string listItem{
    me mode[mItem, mSubItems, mDots, mTagDefn]: itmMode
     >[ (>{"class" >ws > CID:tag > ws > our infon:inf} <=> itmMode=mTagDefn;)
      | (>our infon:inf <=> itmMode=mItem;)
      | ("..." <=> itmMode=mDots; value.format=fUNKNOWN;)
      | (>{"#" >ws >our infon:inf} <=> itmMode=mSubItems;)
      ]
}


string pureInfon{
    >ws
    >[ ( >[ (>uint64: num <=> format = fLiteral;) | ("_" <=> format = fUNKNOWN; num=0; )]   <=>  fType = NUM;)
     | ( >[ (>string: str <=> format = fLiteral;) | ("$" <=> format = fUNKNOWN; str="";)]   <=>  fType = STR;)
     | ( >[ (LISTBODY({,}) <=> format=fLiteral; cursor=cDefault;)
          | (LISTBODY([,]) <=> format=fLiteral; cursor=cGetLast;)
          | (LISTBODY((,)) <=> format=fConcat;  cursor=cDefault;)
          ]  <=>  fType = LST;)
     ]
}

string infon: StartSymbol="true" {
    "!"[opt]:asNot
   >[ ( "?" <=> infMode=isUnknown;)

    | ( >{ "%" >[ ("this" <=> tag="this";) | ("W" <=> tag="W";) | ("prev" <=> tag="prev";) | ("next" <=> tag="next";) ]} <=> infMode=newCursor;)

    | ( >[ (>{ >[("*" <=>  size.inverted = false; ) | ("/" <=>  size.inverted = true; ) ] > me pureInfon: size
               >[("+" <=> value.inverted = false; ) | ("-" <=> value.inverted = true; ) ] > me pureInfon: value
            } <=> size.fType=NUM; value.sizeGiven=true;)
         | (>{ >[("+" <=> value.inverted = false; ) | ("-" <=> value.inverted = true; ) ][opt] > me pureInfon: value}  <=> size.num=0; size.fType=NUM; size.format=fUNKNOWN; value.sizeGiven=false;)
         ]   <=>   infMode=isLiteral;
      )

    | ( >CID:tag <=> infMode=isTag;)
    ]
    >ws

    >our infItr[list]:wrkList

    >{ >[ ("<:" <=> isFunc=true; isFuncArg=false; invertFunc=false;)
        | (":>" <=> isFunc=false; isFuncArg=true; invertFunc=false;)
        | ("<!" <=> isFunc=true; isFuncArg=false; invertFunc=true;)
        | ("!>" <=> isFunc=false; isFuncArg=true; invertFunc=true;)
        ]
        >ws >our infon:aux1
    }[opt]
}

struct pureInfon {}
struct infItr {}
struct infon {}

/-////////////////////////
/- Infon Interface Routines

struct infDeepItr{
    our infItr: crntCursor
    me int: nestLvl
    our infDeepItr: next
    mode[sInvalid, sInited, sNotASeq, sFirstVisit, sSecondVisit, sEOL, sError]: state
    flag: alreadyIncremented   /- When an item is deleted, the cursor idx is already incremented.
    flag: fromDotted

    me bool: isASeq(our infon: i) <- {
        return ((i.value.format==fLiteral and i.value.fType==LST) or i.value.format==fConcat)
    }

    void: initialize(our infon: inf) <- {
        alreadyIncremented<-false
        if(inf==NULL) {state <- sError; return()}
        our infItr:: newParent()
        crntCursor.allocate(inf, newParent, 0, 0)
        if(isASeq(crntCursor.item)){state <- sFirstVisit}
        else{state <- sNotASeq}
        nestLvl <- 0
        me agent: a

    }

    void: initialize(our infItr: InfItr) <- {
        alreadyIncremented<-false
        if(InfItr==NULL or InfItr.item==NULL){state <- sError; return()}
        crntCursor <- InfItr
        if(isASeq(crntCursor.item)){state <- sFirstVisit}
        else{state <- sNotASeq}
        nestLvl <- 0
    }

    me string: toString() <- {
        me string: S <- ""
        me agent: a
        if(this==NULL){S<-S+"NULL"}
        else {S <- S + a.printInfon(crntCursor.item)}
        if(next){S <- S+ " | "+ next.toString()}
        return(S)
    }

    void: goNext() <- {
        switch(state) {
            case sNotASeq:{
                if(crntCursor.parent==NULL){state <- sEOL; return()}
                /- if current item not last item
                me int: nextCursor <- crntCursor.idx +1
                if(nextCursor < crntCursor.parent.item.value.items.size()){
                    crntCursor.idx <- nextCursor
                    crntCursor.item <- crntCursor.parent.item.value.items[crntCursor.idx];   /- NextSibling()
                    if(isASeq(crntCursor.item)){state <- sFirstVisit}
                    else{state <- sNotASeq}
                }else{
                    crntCursor <- crntCursor.parent
                    state <- sSecondVisit
                    nestLvl <- nestLvl-1
                }
            }
            case sFirstVisit:{
                if(crntCursor.item.value.items.size()==0){
                    state <- sSecondVisit
                } else {
                    /-Prepare to iterate children
                    our infItr: tmp
                    Allocate(tmp); tmp.idx<-crntCursor.idx; tmp.parent<-crntCursor.parent; tmp.item <- crntCursor.item; crntCursor.parent  <- tmp;   /- After ptr bug is fixed, Allocate(crntCursor.parent, crntCursor)
                    crntCursor.idx <- 0
                    crntCursor.item <- crntCursor.item.value.items[0];
                    if(isASeq(crntCursor.item)){state <- sFirstVisit}
                    else{state <- sNotASeq}
                    nestLvl <- nestLvl+1
                }
            }
            case sSecondVisit:{
                me int: nextCursor <- crntCursor.idx
                if(alreadyIncremented==0){nextCursor<-nextCursor+1} else {alreadyIncremented<-false}
                if(nestLvl==0){
                    crntCursor.item <- NULL;
                    state <- sEOL
                } else if(nextCursor < crntCursor.parent.item.value.items.size()){
                    crntCursor.idx <- nextCursor
                    crntCursor.item <- crntCursor.parent.item.value.items[crntCursor.idx];   /- NextSibling()
                    if(isASeq(crntCursor.item)){state <- sFirstVisit}
                    else{state <- sNotASeq}
                }else{
                    crntCursor <- crntCursor.parent
                    state <- sSecondVisit
                    nestLvl <- nestLvl-1
                }
            }
            case sInited: {state <- sFirstVisit}
            case sEOL:    {crntCursor.item<-NULL}
            case sInvalid:{crntCursor.item<-NULL;   state <- sError}
            case sError:  {crntCursor.item<-NULL}
            default: {print("Error: invalid iterator state\n")}
        }
    }

    void: copyThisTo(our infDeepItr: to) <- {
        Allocate(to.crntCursor)
        crntCursor.copyThisTo(to.crntCursor)
        to.nestLvl             <- nestLvl
        to.next                <- next
        to.state               <- state
        to.alreadyIncremented  <- alreadyIncremented
        to.fromDotted          <- fromDotted
    }

    our infDeepItr: getNext() <- {
        our infDeepItr: retVal
        Allocate(retVal)
        copyThisTo(retVal)
        retVal.goNext()
        return(retVal)
    }

    void: deleteCrnt() <- {
        if(state == sSecondVisit){
            crntCursor.deleteCrnt()
            alreadyIncremented <- true
        } else {state<-sError}
    }

    our infDeepItr: fillDotChild(their infDeepItr: parent) <- {
        our infon: child
        our infon: infonType <- parent.crntCursor.parent.item.value.listSpec
        if(infonType != NULL){
            child <- infonType /- should be a lazy cow copy
        } else {
            Allocate(child)
            child.value.fType   <- tUNKNOWN
            child.infMode       <- isUnknown
            child.itmMode       <- mItem
            child.value.format  <- fUNKNOWN
           /- me agent: a; print(indent, "else ",a.printInfon(child),"\n")
        }
        our infDeepItr: tentativeItr
        Allocate(tentativeItr)
        Allocate(tentativeItr.crntCursor)
        tentativeItr.crntCursor.idx <- parent.crntCursor.idx
        tentativeItr.crntCursor.parent <- parent.crntCursor.parent
        tentativeItr.crntCursor.item <- child
        tentativeItr.nestLvl <- parent.nestLvl
        tentativeItr.next <- NULL
        tentativeItr.state <- sNotASeq
        tentativeItr.fromDotted <- true

        return(tentativeItr)
    }

    our infDeepItr: getNextDeDotted() <- {
        our infDeepItr: nextItr
        our infDeepItr: retVal
        print("in getNextDeDotted(): ")
        if(!fromDotted){
            print("At-A  ")
            nextItr <- getNext()
            if(nextItr.state == sEOL or nextItr.state == sError){ return(NULL) }
        } else {
            print("At-B  ")
            Allocate(nextItr)
            copyThisTo(nextItr)
        }

        if(fromDotted or nextItr.crntCursor.item.itmMode == mDots){ /- Make a new DOTTED item
            print("At-C  ")
            retVal <- fillDotChild(nextItr)
            if(!fromDotted){print("At-C2  "); retVal.next <- nextItr.getNextDeDotted()}
            else{retVal.next <- nextItr.next}
        }else{
            print("At-D  ")
            retVal <- nextItr
            retVal.next <- NULL
        }
        return(retVal)
    }

}

struct mergeCmd{
    me int: cmd
    me int: sizeDiff
}

#include retriever.dog

struct agent{
    our infon: world
    their infon: context
    me retriever: agenda

    void: testIterator(our infon: i)<-{
        our infDeepItr: testItr
        our infDeepItr: results
        Allocate(testItr)
        testItr.initialize(i)
        withEach count in WHILE(true):{
            results <- testItr.getNextDeDotted()
            if(results == NULL){break()}
            withEach nl in RANGE(0..(results.nestLvl*5)):{print(" ")}
            print("|: ", results.toString(), "\n")
            testItr <- results
        }
    }

    /- me Locale locale

    me bool: joinOnRight(our infon: LHS, our infon: RHS) <- { /- Join RHS into LHS. Return true on success (so need no subscription).
        if(LHS.value.format==fLiteral and RHS.value.format==fLiteral){
            me int: LHSType <- LHS.value.fType
            if(LHSType == RHS.value.fType){
                switch(LHSType){
                    case NUM: {LHS.size.num<-LHS.size.num * RHS.size.num; LHS.value.num<-(LHS.value.num * RHS.size.num) + RHS.value.num;}
                    case STR: {LHS.value.str.append(RHS.value.str);  LHS.size.num<-LHS.value.str.size()}
                    case LST: {}
                    default: {return(false)}
                }
            } else {} /- TODO: handle type conversions
            return(true)
        }
        return(false)
    }

    our infItr: makeInfItr(our infon: i, our infItr: parent, me int: idx) <- {
        our infItr:: ret(i, parent, idx, 0)
        return(ret)
    }

    void: addIdent(our infItr: LHS, our infItr: RHS) <- {
    /- LHS gets RHS as a new ident in its wrkList
        if(RHS.item === LHS.item) {return()}

        /- Set propogation flag for #{} on LHS or RHS
        RHS.target <- asID
        if(RHS.item.itmMode==mSubItems){RHS.target<-asFirstParent}
        if(LHS.item.itmMode==mSubItems){RHS.target<-asFirst}            /- TODO: If bug, check this: What if it is both?
     /-   if(RHS.item.?==asLast){RHS.target<-asLastParent}
     /-   if(LHS.item.?==asLast){RHS.target<-asLast}

        me bool: isDuplicate<-false
  /*       withEach wrkItm in LHS.item.wrkList:{
            if (wrkItm.productionID==RHS.productionID and wrkItm.originPos==RHS.originPos){
                if(wrkItm.SeqPosition==RHS.SeqPos or (ProdType==parseREP and wrkItm.SeqPosition+1 == SeqPos)){
                    isDuplicate<-true
                    /- push cause/pred
                }
            }
        }  */
        if(!isDuplicate){
            LHS.item.wrkList.pushLast(RHS)
        }
    }

    void: prepWorkList(our infItr: ciItr) <- {
        our infon: CI <- ciItr.item
        me int: cursor <- CI.value.cursor
        withEach count in WHILE(true):{
            if(CI.isFunc!=0 or CI.isFuncArg!=0){
                if(CI.isFuncArg!=0){  /- Swap func bady with args
                    me infon: CITmp <- CI
                    CI <deep- CI.aux1
                    CI.isFunc <- true
                    CI.isFuncArg <- false
                    CI.invertFunc <- CITmp.invertFunc

                    Allocate( CI.aux1, CITmp)
                    CI.aux1.aux1 <- NULL
                    CI.aux1.isFunc <- true
                    CI.aux1.isFuncArg <- false
                }
                our infon: tmp
                if(CI.invertFunc!=0){tmp <- CI.getLastItem()}
                else {tmp <- CI.getFirstItem()}
                tmp.wrkList.pushLast(makeInfItr(CI.aux1, NULL, 0))
                CI.aux1 <- NULL
            }
            if(cursor != cDefault){
                CI.value.cursor <- cDefault
                our infon: aux Allocate(aux)
                aux <deep- CI
                normalize(aux)
                our infon: newTop
                switch(cursor){  /- TODO: ensure that, when possible, these return subscriptions if needed. e.g., for when last is tentative or a tag or concat.
                    case cGetLast   : {newTop <- aux.getLastItem()}
                    case cGetFirst  : {newTop <- aux.getFirstItem()}
         /-           case cGetMarked : {newTop <- aux.getMarked()}
                }
                if(newTop!=NULL) {
                    CI <deep- newTop
                    cursor <- CI.value.cursor
                }
            } else {break()}
        }
    }


    me int: PropogateIndirectItem(our infItr: LHS, our infItr: RHS) <- {
    /- HANDLE #{}, [....] and other cases of identities of child nodes:
        me int: ret <- 0
        their pureInfon: LHS_tmp <- LHS.item.value
        if(LHS_tmp.fType==LST and LHS_tmp.items.size()>0){
            if(RHS.target == asFirst)       {addIdent(makeInfItr(LHS_tmp.items[0], LHS, 0), RHS);  ret<-1;}
            /- if(RHS.target == asLast or LHS.target==asLastParent)   {addIdent(makeInfItr(LHS_tmp.items[LHS.item.value.items.size()-1], LHS, LHS_tmp.items.size()-1), RHS);  ret<-1;}
        }
        their pureInfon: RHS_tmp <- RHS.item.value
        if(RHS_tmp.fType==LST and RHS_tmp.items.size()>0){
            if(RHS.target == asFirstParent) {addIdent(LHS, makeInfItr(RHS.item.value.items[0], RHS, 0));  ret<-1;}
        }
        return(ret)
    }

    void: DO_COPY(their pureInfon: from, their pureInfon: to) <- {
        me int: fromsType <- from.fType
        if(fromsType==to.fType){
            to.format<-from.format
            to.sizeGiven <- from.sizeGiven
            switch(fromsType){
                case NUM: {to.num <- from.num}
                case STR: {to.str <- from.str}
                case LST: {}
            }
        } else {  /- TODO: Do Convert + copy
        }
    }

    const int: matchFailed  <- 1
    const int: wasVerified  <- 2
    const int: copyLHStoRHS <- 3
    const int: copyRHStoLHS <- 4

    me int: matchLength(me string: A, me string: B)<-{
        me int: lastChar
        me int: retVal <-0
        lastChar <- min(A.size(), B.size())
        if (lastChar==0){return(0)}
        withEach pos in RANGE(0..lastChar):{
            if(A[pos]!= B[pos]){
                return(pos-1)
            }
        }
        return(lastChar)
    }

    me mergeCmd: DO_VERIFY(their pureInfon: LHS, their pureInfon: RHS) <- {
        me int: RHSType <- RHS.fType
        /-print("            DO_VERIY LHS.fType: ", LHS.fType," RHS.fType: ", RHS.fType,"\n")
        me mergeCmd: retVal
        if(RHSType==LHS.fType){
            switch(RHSType){
                case NUM: {
                    if(LHS.num == RHS.num){
                        retVal.cmd <-1
                        retVal.sizeDiff <- 0
                    }
                }
                case STR: {
                    me int: LSize <- LHS.str.size()
                    me int: RSize <- RHS.str.size()
                    me int: matchLen <- matchLength(LHS.str, RHS.str)
                    if(matchLen==-1 or (matchLen < LSize and matchLen < RSize)){retVal.cmd <- matchFailed}
                    else{retVal.sizeDiff <- RSize - LSize; retVal.cmd <- wasVerified}
                }
                case LST: {
                    /- TODO: Is it sometimes OK to skip this or check each member at once?
                }
                default:{print("unknown fType in DO_VERIFY\n")}
            }
        } else {  /- TODO: Do Convert + verify
        }
        return(retVal)
    }

    void: CO_SUBSCRIBE_VALUES(their infon: from, their infon: to) <- {}
    void: CO_SUBSCRIBE_SIZES(their infon: from, their infon: to) <- {}

    our infDeepItr: tryMERGE(our aItem: agendaItem) <- {
        print("    tryMERGE: \n")
        our infon: LHS <- agendaItem.LHS.crntCursor.item
        our infon: RHS <- agendaItem.RHS.crntCursor.item
        our infDeepItr: remainder <- NULL
        if(PropogateIndirectItem(agendaItem.LHS.crntCursor, agendaItem.RHS.crntCursor) > 0){return(NULL)}

        me int: copyItemsTypeToCI <- 0
        if(LHS.value.fType==tUNKNOWN and RHS.value.fType!=tUNKNOWN){
            copyItemsTypeToCI <- 1
        }

        me bool: LooseType<-false   /- Strict or loose typing?
        me bool: reject <- false    /- ReturnStatus

        me int: actionCopySize  <- 0
        me int: actionCopyValue <- 0
        me mergeCmd: sizeStatus
        if(! LooseType){
            me int: LHSFormat <- LHS.size.format
            me int: RHSFormat <- RHS.size.format
            print("        ! LooseType: ", LHSFormat, " || ", RHSFormat,"\n")
            if(LHSFormat != fUNKNOWN and RHSFormat != fUNKNOWN){sizeStatus <- DO_VERIFY(LHS.value, RHS.value)}
            else if(LHSFormat != fUNKNOWN and RHSFormat == fUNKNOWN){actionCopyValue <- copyLHStoRHS }
            else if(LHSFormat == fUNKNOWN and RHSFormat != fUNKNOWN){actionCopyValue <- copyRHStoLHS }
            else if(LHSFormat == fUNKNOWN and RHSFormat == fUNKNOWN){CO_SUBSCRIBE_VALUES(LHS, RHS)}
        }
        me mergeCmd: valueStatus
        if(!reject){
            me int: LHSFormat <- LHS.value.format
            me int: RHSFormat <- RHS.value.format
            print("        ! reject: ", LHSFormat, " || ", RHSFormat,"\n")
            if(LHSFormat != fUNKNOWN and RHSFormat != fUNKNOWN){
                valueStatus <- DO_VERIFY(LHS.value, RHS.value)
            }
            else if(LHSFormat != fUNKNOWN and RHSFormat == fUNKNOWN){actionCopyValue <- copyLHStoRHS }
            else if(LHSFormat == fUNKNOWN and RHSFormat != fUNKNOWN){actionCopyValue <- copyRHStoLHS }
            else if(LHSFormat == fUNKNOWN and RHSFormat == fUNKNOWN){CO_SUBSCRIBE_VALUES(LHS, RHS)}
        }

        if (valueStatus.cmd == wasVerified and RHS.value.fType == STR){
            print("        wasVerified: ", RHS.value.str, "\n")
            me int: sizeDiff <- valueStatus.sizeDiff
            me int: rSize <- RHS.value.str.size()
            if(sizeDiff > 0 ){
                me string: remainderStr <- RHS.value.str.subStr(rSize -sizeDiff, sizeDiff)
                our infon: remainderInfon; Allocate(remainderInfon,remainderStr)
                Allocate(remainder)
                agendaItem.RHS.copyThisTo(remainder)
                Allocate(remainder.crntCursor)
                remainder.crntCursor.item <- remainderInfon
                print("####### REMAINDER:", remainderStr,"\n")

            }/- TODO: finish case where remainder is on the left

        }
        if(valueStatus.cmd==matchFailed){agendaItem.actionCommand<-matchFailed; reject<-true}
        if(reject){return(NULL)}

        /- Next list op: process remainder/next | descend
  /*       if(LooseType and LHS.value.fType==STR and RHS.value.fType==STR ){
            /- If LooseType, calculate partial remainders

            Allocate(remainder)
            remainder.initialize(tmpRemainder)
        }  */
        return(remainder)
    }

    void: addWorkItemsToAgenda(me int: priority, our aItem: pred, our infDeepItr: itrToProcess)<-{
        our infon: i <- itrToProcess.crntCursor.item
        withEach workItem in i.wrkList:{
            our infDeepItr: newIDP
            Allocate(newIDP)
            newIDP.initialize(workItem)
            our aItem: newItem
            Allocate(newItem)
            newItem.init(0,itrToProcess,newIDP)
            newItem.predecessors.pushLast(pred)
            agenda.addItem(newItem)
        }
    }


    void: normalize(our infon: i) <- {
      /-  testIterator(i)
      /-  exit(2)
        our infDeepItr: ii
        Allocate(ii)
        ii.initialize(i)
        agenda.parentAgent <- self
        addWorkItemsToAgenda(0, NULL, ii)
        agenda.processAgenda()
        /-print("____________________________________________________\nEND NORMALIZE\n")
    }

/* TODO: Check these for changes
    our infItr[our list]: doWorkList(our infItr: ciItr) <- {}
    our infItr[our list]: fetchNodesNormalForm(their infDeepItr: cii) <- {}
    void: normalize(our infon: i) <- {}
*/

    me string: fetchAsStr(their infon: i) <- {return(i.value.str)}
    me uint64: fetchAsNum(their infon: i) <- {return(i.value.num)}
    our infon: fetchAsList(their infon: i, me int: idx) <- {return(i.value.items[idx])}

    me bool: loadAndParseData() <- {
        me string: data <- readFileAsString("data.pr")
        if(data==""){
            print("File Error!\n\n")
            exit(2)
        }

        me EParser: parser
        parser.populateGrammar()
        parser.initPosStateSets(parser.infon_str, data)

        parser.doParse()
        if(parser.doesParseHaveError()){
            print("\nParse Error:", parser.errorMesg, " at line ", parser.errLineNum,":", parser.errCharPos, "\n")
            exit(2)
        } else {print("\nParse Succeeded\n")}

        our stateRec: topItem <- parser.resolve(parser.lastTopLevelItem, "")
        /-    parser.displayParse(topItem, "")
        Allocate(world)
        parser.Extract_infon_to_infon(topItem, world)
        /-    trackerMgr.allData.topInfon <- settingsAndData

        return(true)
    }


    me string: printPureInfon(their pureInfon: pure, their pureInfon: size) <- {
        me string: S <- ""
        me bool: showSize <- (size!=NULL and (pure.sizeGiven !=0) and (pure.fType!=LST or pure.format!=fLiteral))
        if(showSize){S <- S+printPureInfon(size, NULL)}
        if(pure.inverted){
            if(size!=NULL){S <- S+"-"}
            else if(pure.isLstSize==0){S <- S+"/"}
        } else{
                if(showSize){S <- S+"+"}
                else if(size==NULL and pure.isLstSize==0){S <- S+"*"}
        }
        if(pure.fType == NUM){
            if(pure.format == fUNKNOWN){S<-S+"_"}
            else{
                me uint64:N <- pure.num
                S <- S+ toString(N)
            }
        } else if(pure.fType == STR){
            if(pure.format == fUNKNOWN){S<-S+"$"}
            else{
                me string: str <- pure.str
                S <- S+"'"+ str + "'"
            }
        } else if(pure.fType == LST){
            me bool: showBar<-false
            me string: endBrkt<-"}"
            if(pure.cursor == cGetLast)     {S<-S+"["  endBrkt<-"]"}
            else if(pure.format == fConcat) {S<-S+"("  endBrkt<-")"}
            else {S <- S+("{")}
            if(pure.timelike){S <- S+"T "}
            if(pure.size!=NULL) {pure.size.isLstSize <- true}
            if(pure.size!=NULL and pure.format==fLiteral){S <- S+"~"+printPureInfon(pure.size, NULL) showBar<-true}
            if(pure.listSpec){
                if(showBar){S <- S+" "}
                S <- S + printInfon(pure.listSpec)
                showBar<-true
            }
            if(showBar){S <- S+"| "}
            withEach item in pure.items:{
                if(item_key>0){S <- S+" "}
                S <- S+printInfon(item)
            }
            S <- S+endBrkt
        } else {print("\nError: Unknown fType in printPureInfon(): ", pure.fType,"\n") exit(2)}
        return(S)
    }

    me string: printInfon(our infon: i) <- {
        me string: S <- ""
        if(i ==  NULL){return("NULL")}
        if(i.itmMode==mSubItems){S<-S+"#"}
        else if(i.itmMode==mDots){S<-S+"..." return(S)}
        else if(i.itmMode==mTagDefn){S<-S+"class "+i.tag+" "}

        if(i.asNot){S <- S+"!"}
        if(i.infMode==isUnknown){S <- S+"?"}
        else if(i.infMode==newCursor){S <- S+"%"+i.tag}
        else if(i.infMode==isTag){S <- S+i.tag}
        else if(i.infMode==isLiteral){
             S <- S+printPureInfon(i.value, i.size)
        } else {print("Error In PrintInfon()\n") exit(2)}

        if(i.isNormed ==0){
            withEach WorkItm in i.wrkList:{
                S <- S+" = "+printInfon(WorkItm.item)
            }

            if (i.isFunc){
                if(i.invertFunc){S <- S+"<!" + printInfon(i.aux1)}
                else{S <- S+"<:" + printInfon(i.aux1)}
            } else if(i.isFuncArg !=0){
                if(i.invertFunc){S <- S+"!>" + printInfon(i.aux1)}
                else{S <- S+":>" + printInfon(i.aux1)}
            }
        }
        return(S)
    }

}
/-///////////////////////////////////////
do codeDataDisplay(infon, TAG_infonDumpMode)
