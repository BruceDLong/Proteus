/-////////////////  Structures For Working with Proteus
/- Proteus.dog

LinuxTestBuild: Platform='Linux' CPU='amd64' Lang='CPP' testMode='makeTests';
/-LinuxBuild:     Platform='Linux' CPU='amd64' Lang='CPP' optimize='speed';

FileName = "Proteus"
Version = "0.8"
CopyrightMesg = "Copyright (c) 2015-2016 Bruce Long"
Authors = "Bruce Long"
Description = "Proteus Engine"
ProgramOrLibrary = "program"
TestSpec = "ProteusTests.dog"

featuresNeeded = [ ]

LicenseText = `This file is part of the "Proteus Language suite" All Rights Reserved.`
infonDumpMode='text'

do ManageCmdLine(cmd)

struct GLOBAL{
    our infon[multimap string]: classDefs
    void: registerClass(our infon: classInfon) <- {
        print("REGISTER:", classInfon.tag, "\n")
        classDefs.insert(classInfon.tag, classInfon)
    }
}

model pureInfon{
    me mode[tUnknown, NUM, STR, LST]: fType
    me mode[fUnknown, fConcat, fLiteral]: format
    me mode[fDefault, fFloat, fFraction, fSingleQuote, fHereDoc, fSizedBin]: literalFormat
    me mode[bDec, bBin, bHex]:numBase
    me mode[cDefault, cGetLast, cGetFirst, cGetMarked]: cursor
    me flag: inverted
    me flag: timelike
    flag: sizeGiven
    flag: isLstSize
    me uint64: num
    me string: str
    our infon[list]: items
    our infon: listSpec

    our pureInfon: size

    me none: pureInfon(me int: N) <- {
        fType <- NUM
        format <- fLiteral
        num <- N
    }
    me none: pureInfon(me string: S) <- {
        fType <- STR
        format <- fLiteral
        str <- S

    }
    void: deleteNth(me int: toDel) <- {items.erase(items.nthItr(toDel))}
    void: clear() <- {num<-0 str<-'' items.clear() listSpec<-NULL}
}
model infon{
    flag: asNot
    flag: isFunc
    flag: isFuncArg
    flag: invertFunc
    mode[mItem, mSubItems, mDots, mTagDefn]:      itmMode
    mode[isUnknown, newCursor, isTag, isLiteral]: infMode
    mode[cNone, cThis, cWorld, cContext, cPrev, cNext]:  cursorMode
    me pureInfon: size
    me pureInfon: value
    flag: isNormed
    flag: hasSelector
    our infon: aux1
    our infon: aux2
    me string: tag
    our infItr[list]: wrkList
    flag: wrkLstPreped
    flag: wrkItmsAdded
    /-flag: wrkListIsAlts

    our infon: getLastItem() <- {
        /- ASSERT(Should be already normed and a non-concat list)
        our infon: lastItem
        if(value.items.size()>0) {
            lastItem <- value.items[value.items.size()-1]
        }  else {return(NULL)}
        switch(lastItem.itmMode){
            case mItem:{
                return(lastItem)
            }
            case mSubItems:{return(lastItem.getLastItem())}
            case mDots:{exit(3)}
            case mTagDefn:{exit(4)}
        }
        return(lastItem)
    }
    our infon: getFirstItem() <- {
        /- ASSERT(Should be already normed and a non-concat list)
        our infon: firstItem
        if(value.items.size()>0) {
            firstItem <- value.items[0]
        }  else {return(NULL)}
        switch(firstItem.itmMode){
            case mItem:{
                return(firstItem)
            }
            case mSubItems:{return(firstItem.getFirstItem())}
            case mDots:{exit(3)}
            case mTagDefn:{exit(4)}
        }
        return(firstItem)
    }
    void: deleteNth(me int: toDel) <- {
        /- Low level delete of an item in list. Does not update size.
        if(value.fType==LST){
            value.deleteNth(toDel)
        }
    }
    void: clear() <- {size.clear() value.clear()}
    me none: infon(me string: S) <- {
        infMode <- isLiteral
        itmMode <- mItem
        size <- pureInfon(S.size())
        value <- pureInfon(S)
    }
}
model infItr{
    our infon: item
    our infItr: parent
    me  int: idx
    mode [asID, asFirst, asLast, asMarked, asFirstParent, asLastParent, asMarkedParent]: target
    flag: wasProcessed

    me bool: goNext() <- {
        withEach count in WHILE(true):{
            if(parent==NULL or item==NULL or idx+1 >= parent.item.value.items.size()){
                item <- NULL  /- Signify EOL
                return(false)
            }
            idx  <- idx+1
            item <- parent.item.value.items[idx]
            if(item.itmMode == mTagDefn){
                registerClass(item)
            }else {
                return(true)
            }
        }
    }

    our infon: getPrevPtr() <- {
        if(parent==NULL or item==NULL or idx==0) {return(NULL)}
        return(parent.item.value.items[idx-1])
    }

    void: copyThisTo(our infItr: to) <- {
        to.item <- item
        to.parent <- parent
        to.idx <- idx
    }

    void: deleteCrnt() <- {
        parent.item.deleteNth(idx)
        idx <- idx-1
    }

    void: deleteNth(me int: N) <- {
        parent.item.deleteNth(N)
        if(idx >= N){N <- N-1}
    }

    void: insertAtCursor(our infon: i) <- {
        parent.item.value.items.insert(parent.item.value.items.nthItr(idx), i)
        idx <- idx+1
    }

    void: insertBeforeNth(me int: IDX, our infon: i) <- {
        parent.item.value.items.insert(parent.item.value.items.nthItr(IDX), i)
        if(idx>IDX){ idx <- idx+1}
    }

}

#define LISTBODY(LEFT_BRKT, RIGHT_BRKT) <% >{ "LEFT_BRKT" "T"[opt]:timelike  >me listInfo[opt] >our listItem[list]: items >ws "RIGHT_BRKT" } %>
string infItr{
    "=" > ws > our infon:item
}
string listInfo[
        >{ >{"~" >ws >our pureInfon: size}[opt]:Size >ws "C" >ws >our infon:listSpec "|"[opt]}
     |  >{ >{"~" >ws >our pureInfon: size}[opt]:Size         >ws >our infon:listSpec "|"}
     |  >{ "~" >ws >our pureInfon: size >ws }
]
string listItem{
    me mode[mItem, mSubItems, mDots, mTagDefn]: itmMode
     >[ (>{"class" >ws > CID:tag > ws ":" > ws > our infon:inf} <=> itmMode=mTagDefn;)
      | (>our infon:inf <=> itmMode=mItem;)
      | ("..." <=> itmMode=mDots; value.format=fUnknown;)
      | (>{"#" >ws >our infon:inf} <=> itmMode=mSubItems;)
      ]
}
string pureInfon{
    >ws
    >[ ( >[ (>uint64: num <=> format = fLiteral;) | ("_" <=> format = fUnknown; num=0; )]   <=>  fType = NUM;)
     | ( >[ (>string: str <=> format = fLiteral;) | ("$" <=> format = fUnknown; str="";)]   <=>  fType = STR;)
     | ( >[ (LISTBODY({,}) <=> format=fLiteral; cursor=cDefault;)
          | (LISTBODY([,]) <=> format=fLiteral; cursor=cGetLast;)
          | (LISTBODY((,)) <=> format=fConcat;  cursor=cDefault;)
          ]  <=>  fType = LST;)
     ]
}
string infon: StartSymbol="true" {
    "!"[opt]:asNot
   >[ ( "?" <=> infMode=isUnknown;)

    | ( >{ "%" >[ ("this" <=> cursorMode=cThis;) | ("W" <=> cursorMode=cWorld;) | ("prev" <=> cursorMode=cPrev;) | ("next" <=> cursorMode=cNext;) ]} <=> infMode=newCursor;)

    | ( >[ (>{ >[("*" <=>  size.inverted = false; ) | ("/" <=>  size.inverted = true; ) ] > me pureInfon: size
               >[("+" <=> value.inverted = false; ) | ("-" <=> value.inverted = true; ) ] > me pureInfon: value
            } <=> size.fType=NUM; value.sizeGiven=true;)
         | (>{ >[("+" <=> value.inverted = false; ) | ("-" <=> value.inverted = true; ) ][opt] > me pureInfon: value}  <=> size.num=0; size.fType=NUM; size.format=fUnknown; value.sizeGiven=false;)
         ]   <=>   infMode=isLiteral;
      )

    | ( >CID:tag <=> infMode=isTag;)
    ]
    >ws

    >our infItr[list]:wrkList

    >{ >[ ("<:" <=> isFunc=true; isFuncArg=false; invertFunc=false;)
        | (":>" <=> isFunc=false; isFuncArg=true; invertFunc=false;)
        | ("<!" <=> isFunc=true; isFuncArg=false; invertFunc=true;)
        | ("!>" <=> isFunc=false; isFuncArg=true; invertFunc=true;)
        ]
        >ws >our infon:aux1
    }[opt]
}

struct pureInfon {}
struct infItr {}
struct infon {}

/-////////////////////////
/- Infon Interface Routines

model infDeepItr{
    our infItr: crntCursor
    me int: nestLvl
    our infDeepItr: next
    mode[sInvalid, sInited, sNotASeq, sFirstVisit, sSecondVisit, sEOL, sError]: state
    flag: alreadyIncremented   /- When an item is deleted, the cursor idx is already incremented.
    flag: fromDotted
    flag: isAList

    me bool: isASeq(our infon: i) <- {
        /-if(i.value.format==fConcat){print("ENTERING CONCAT\n")}
        return ((i.value.format==fLiteral and i.value.fType==LST) or i.value.format==fConcat)
    }

    void: setListSeqFlags() <- {
        if(isASeq(crntCursor.item)){state <- sFirstVisit; isAList <- true}
        else{state <- sNotASeq; isAList <- false}
    }

    void: initialize(our infon: inf) <- {
        alreadyIncremented<-false
        if(inf==NULL) {state <- sError; return()}
        our infItr:: newParent()
        crntCursor.allocate(inf, newParent, 0, 0)
        setListSeqFlags()
        nestLvl <- 0
        me agent: a

    }

    void: initialize(our infItr: InfItr) <- {
        alreadyIncremented<-false
        if(InfItr==NULL or InfItr.item==NULL){state <- sError; return()}
        crntCursor <- InfItr
        setListSeqFlags()
        nestLvl <- 0
    }

    me string: ToString() <- {
        me string: S <- ""
        me agent: a
        if(this==NULL){S<-S+"NULL"}
        else if(state == sEOL){S<-S+" sEOL"}
        else if(state == sError){S<-S+" sError"}
        else {
            S <- S + a.printInfon(crntCursor.item)
            if(next){S <- S+ " | "+ next.ToString()}
        }
        return(S)
    }

    void: goNext() <- {
        switch(state) {
            case sNotASeq: sSecondVisit:{  /- Get the next item or set EOL
                if(crntCursor.parent==NULL or nestLvl==0){state <- sEOL; return()}
                if(crntCursor.goNext()){
                    setListSeqFlags()
                }else{
                    crntCursor <- crntCursor.parent
                    state <- sSecondVisit
                    isAList <- true
                    nestLvl <- nestLvl-1
                }
            }
            case sFirstVisit:{
                if(crntCursor.item.value.items.size()==0){
                    state <- sSecondVisit
                    isAList <- true
                } else {
                    /-Prepare to iterate children
                    our infItr: tmp
                    Allocate(tmp); tmp.idx<-crntCursor.idx; tmp.parent<-crntCursor.parent; tmp.item <- crntCursor.item; crntCursor.parent  <- tmp;   /- TODO: After ptr bug is fixed, Allocate(crntCursor.parent, crntCursor)
                    crntCursor.idx <- 0
                    crntCursor.item <- crntCursor.item.value.items[0];
                    setListSeqFlags()
                    nestLvl <- nestLvl+1
                }
            }
            case sInited: {state <- sFirstVisit}
            case sEOL:    {crntCursor.item<-NULL}
            case sInvalid:{crntCursor.item<-NULL;   state <- sError}
            case sError:  {crntCursor.item<-NULL}
            default: {print("Error: invalid iterator state:", state, "\n")}
        }
    }

    void: copyThisTo(our infDeepItr: to) <- {
        Allocate(to.crntCursor)
        if(crntCursor==NULL){to.crntCursor <- NULL}
        else{crntCursor.copyThisTo(to.crntCursor)}
        to.nestLvl             <- nestLvl
        to.next                <- next
        to.state               <- state
        to.alreadyIncremented  <- alreadyIncremented
        to.fromDotted          <- fromDotted
        to.isAList             <- isAList
    }

    our infDeepItr: getNext() <- {
        our infDeepItr: retVal
        Allocate(retVal)
        copyThisTo(retVal)
        retVal.goNext()
        return(retVal)
    }

    void: deleteCrnt() <- {
        crntCursor.deleteCrnt()
        alreadyIncremented <- true
    }

    our infDeepItr: fillDotChild(their infDeepItr: parent) <- {
        our infon: child
        our infon: infonType <- parent.crntCursor.parent.item.value.listSpec
        if(infonType != NULL){
            child <- infonType /- should be a lazy cow copy
        } else {
            Allocate(child)
            child.value.fType   <- tUnknown
            child.infMode       <- isUnknown
            child.itmMode       <- mItem
            child.value.format  <- fUnknown
        }
        our infDeepItr: tentativeItr
        Allocate(tentativeItr)
        Allocate(tentativeItr.crntCursor)
        tentativeItr.crntCursor.idx <- parent.crntCursor.idx
        tentativeItr.crntCursor.parent <- parent.crntCursor.parent
        tentativeItr.crntCursor.item <- child
        tentativeItr.nestLvl <- parent.nestLvl
        tentativeItr.next <- NULL
        tentativeItr.setListSeqFlags() /- Set state, etc.
        tentativeItr.fromDotted <- true

        return(tentativeItr)
    }

    our infDeepItr: getNextDeDotted() <- {
        our infDeepItr: nextItr
        our infDeepItr: retVal

        if(!fromDotted){
            nextItr <- getNext()
            if(nextItr.state == sError){return(NULL) }
        } else {
            Allocate(nextItr)
            copyThisTo(nextItr)
        }

        if(nextItr.state == sEOL){
          Allocate(retVal)
          retVal.state <- sEOL
          retVal.isAList <- nextItr.isAList
          retVal.next <- NULL
        } else if(fromDotted or nextItr.crntCursor.item.itmMode == mDots){  /- The new item is nullable so also return the follower.
            retVal <- fillDotChild(nextItr)
            if(!fromDotted){retVal.next <- nextItr.getNextDeDotted()} /- Caused by "..."
            else{retVal.next <- nextItr.next}                         /- Caused by a result of "..."
        } else {
            retVal <- nextItr
            retVal.next <- NULL
        }
        return(retVal)
    }

}
struct infDeepItr {}
struct mergeCmd{
    me int: cmd
    me int: sizeDiff
}

#include retriever.dog

struct agent{
    our infon: world
    our infon: context
    me retriever: agenda
    /- me Locale locale

    void: testIterator(our infon: i)<-{
        our infDeepItr: testItr
        our infDeepItr: results
        Allocate(testItr)
        testItr.initialize(i)
        withEach count in WHILE(true):{
            results <- testItr.getNextDeDotted()
            if(results == NULL){break()}
            withEach nl in RANGE(0..(results.nestLvl*5)):{print(" ")}
            print("|: ", results.ToString(), "\n")
            testItr <- results
        }
    }

    me bool: joinOnRight(our infon: LHS, our infon: RHS) <- { /- Join RHS into LHS. Return true on success (so need no subscription).
        if(LHS.value.format==fLiteral and RHS.value.format==fLiteral){
            me int: LHSType <- LHS.value.fType
            if(LHSType == RHS.value.fType){
                switch(LHSType){
                    case NUM: {LHS.size.num<-LHS.size.num * RHS.size.num; LHS.value.num<-(LHS.value.num * RHS.size.num) + RHS.value.num;}
                    case STR: {LHS.value.str.append(RHS.value.str);  LHS.size.num<-LHS.value.str.size()}
                    case LST: {}
                    default: {return(false)}
                }
            } else {} /- TODO: handle type conversions
            return(true)
        }
        return(false)
    }

    our infItr: makeInfItr(our infon: i, our infItr: parent, me int: idx) <- {
        our infItr:: ret(i, parent, idx, 0)
        return(ret)
    }

    void: addIdent(our infItr: LHS, our infItr: RHS) <- {
    /- LHS gets RHS as a new ident in its wrkList
        if(RHS.item === LHS.item) {return()}

        /- Set propogation flag for #{} on LHS or RHS
        RHS.target <- asID
        if(RHS.item.itmMode==mSubItems){RHS.target<-asFirstParent}
        if(LHS.item.itmMode==mSubItems){RHS.target<-asFirst}            /- TODO: If bug, check this: What if it is both?
     /-   if(RHS.item.?==asLast){RHS.target<-asLastParent}
     /-   if(LHS.item.?==asLast){RHS.target<-asLast}

        LHS.item.wrkList.pushLast(RHS)
    }

    void: prepWorkList(our infDeepItr: ciItr) <- {
        our infon: CI <- ciItr.crntCursor.item
        if (CI.wrkLstPreped){return()}else{CI.wrkLstPreped <- 1}
        withEach count in WHILE(true):{
            our infon: newTop<-NULL
            if(CI.isFunc or CI.isFuncArg){
               /- print("HANDLING FUNCTION ARGS\n")
                if(CI.isFuncArg!=0){  /- Swap func body with args
                    me infon: CITmp <- CI
                    CI <deep- CI.aux1
                    CI.isFunc <- false
                    CI.isFuncArg <- false
                    CI.invertFunc <- CITmp.invertFunc

                    Allocate( CI.aux1, CITmp)
                    CI.aux1.aux1 <- NULL
                    CI.aux1.isFunc <- false
                    CI.aux1.isFuncArg <- false
                }
                our infon: tmp
                if(CI.invertFunc!=0){tmp <- CI.getLastItem()}
                else {tmp <- CI.getFirstItem()}
                tmp.wrkList.pushLast(makeInfItr(CI.aux1, NULL, 0))
                CI.aux1 <- NULL
                if(CI.value.fType==LST){ciItr.state <- sFirstVisit}
                else if(CI.value.fType!=LST){ciItr.state <- sNotASeq}
            }
            me int: cursor <- CI.value.cursor
            if(cursor != cDefault){
              /- print("HANDLING FUNCTION USAGE\n")
                CI.value.cursor <- cDefault
                our infon: aux Allocate(aux)
                aux <deep- CI
                normalize(aux)
                switch(cursor){  /- TODO: ensure that, when possible, these return subscriptions if needed. e.g., for when last is tentative or a tag or concat.
                    case cGetLast   : {newTop <- aux.getLastItem()}
                    case cGetFirst  : {newTop <- aux.getFirstItem()}
         /-           case cGetMarked : {newTop <- aux.getMarked()}
                }
            } else if(CI.infMode==newCursor) {
                switch(CI.cursorMode){
                    case cThis:   {} /-{newTop <- thisItem}
                    case cWorld:  {newTop <- world}
                    case cContext:{newTop <- context}
                    case cPrev:   {} /-{newTop <- previous}
                    case cNext:   {} /-{newTop <- next}
                }
            } else {break()}

            if(newTop!=NULL) {
                CI <deep- newTop
            }
        }
    }

    me int: PropogateIndirectItem(our infItr: LHS, our infItr: RHS) <- {
    /- HANDLE #{}, [....] and other cases of identities of child nodes:
        me int: ret <- 0
        their pureInfon: LHS_tmp <- LHS.item.value
        if(LHS_tmp.fType==LST and LHS_tmp.items.size()>0){
            if(RHS.target == asFirst)       {addIdent(makeInfItr(LHS_tmp.items[0], LHS, 0), RHS);  ret<-1;}
            /- if(RHS.target == asLast or LHS.target==asLastParent)   {addIdent(makeInfItr(LHS_tmp.items[LHS.item.value.items.size()-1], LHS, LHS_tmp.items.size()-1), RHS);  ret<-1;}
        }
        /-their pureInfon: RHS_tmp <- RHS.item.value
        if(RHS.item.value.fType==LST and RHS.item.value.items.size()>0){
            if(RHS.target == asFirstParent) {addIdent(LHS, makeInfItr(RHS.item.value.items[0], RHS, 0));  ret<-1;}
        }
        return(ret)
    }

    void: DO_COPY(their pureInfon: from, their pureInfon: to) <- {
        me int: fromsType <- from.fType
        if(fromsType==to.fType){
            to.format<-from.format
            to.sizeGiven <- from.sizeGiven
            switch(fromsType){
                case NUM: {to.num <- from.num}
                case STR: {to.str <- from.str}
                case LST: {}
            }
        } else {  /- TODO: Do Convert + copy
        }
    }

    me int: matchLength(me string: A, me string: B)<-{
        me int: lastChar
        me int: retVal <-0
        lastChar <- min(A.size(), B.size())
        if (lastChar==0){return(0)}
        withEach pos in RANGE(0..lastChar):{
            if(A[pos]!= B[pos]){
                return(pos-1)
            }
        }
        return(lastChar)
    }

    const int: matchFailed  <- 1
    const int: wasVerified  <- 2

    me mergeCmd: DO_VERIFY(their pureInfon: LHS, their pureInfon: RHS) <- {
        me int: RHSType <- RHS.fType
        me mergeCmd: retVal
        if(RHSType==LHS.fType){
            switch(RHSType){
                case NUM: {
                    if(LHS.num == RHS.num){
                        retVal.cmd <- wasVerified
                        retVal.sizeDiff <- 0
                    } else {retVal.cmd <- matchFailed}
                }
                case STR: {
                    me int: LSize <- LHS.str.size()
                    me int: RSize <- RHS.str.size()
                    me int: matchLen <- matchLength(LHS.str, RHS.str)
                    if(matchLen==-1 or (matchLen < LSize and matchLen < RSize)){retVal.cmd <- matchFailed}
                    else{retVal.sizeDiff <- RSize - LSize; retVal.cmd <- wasVerified}
                }
                case LST: {
                    /- TODO: Is it sometimes OK to skip this or check each member at once?
                }
                default:{print("unknown fType in DO_VERIFY\n")}
            }
        } else {  /- TODO: Do Convert + verify
        }
        return(retVal)
    }

    void: CO_SUBSCRIBE_VALUES(their infon: from, their infon: to) <- {}
    void: CO_SUBSCRIBE_SIZES(their infon: from, their infon: to) <- {}

    our aItem: stitch(our aItem: crntItem, me string: indent)<-{
        /-print(indent + "STITCH: ", crntItem.printAgendaItem(),"\n")
        our aItem: retVal <- NULL
        me bool: copyRHSTypeToLHS <- crntItem.copyRHSTypeToLHS
        me int: sizeAction <- crntItem.sizeAction
        me int: valueAction <- crntItem.valueAction             crntItem.valueAction <- false
        our infDeepItr: LHS <- crntItem.LHS
        our infDeepItr: RHS <- crntItem.RHS
        switch(LHS.state) {
            case sNotASeq:{
                if(RHS.crntCursor!=NULL and RHS.state!=sSecondVisit){
                    if(true or copyRHSTypeToLHS){ LHS.crntCursor.item.value.fType <- RHS.crntCursor.item.value.fType}
                    LHS.crntCursor.item.infMode <- RHS.crntCursor.item.infMode

                    if(sizeAction == copySizeRHStoLHS)     {DO_COPY(RHS.crntCursor.item.size, LHS.crntCursor.item.size)}
                    else if(sizeAction == copySizeLHStoRHS){DO_COPY(LHS.crntCursor.item.size, RHS.crntCursor.item.size)}

                    if(valueAction == copyValueRHStoLHS)     {DO_COPY(RHS.crntCursor.item.value, LHS.crntCursor.item.value)}
                    else if(valueAction == copyValueLHStoRHS){DO_COPY(LHS.crntCursor.item.value, RHS.crntCursor.item.value)}
                }
                retVal <- crntItem.successor
            }
            case sFirstVisit:{
                our aItem: nextItem <- crntItem.successor
                me int: crntIDX <- 0
                our infon: crntInfon <- crntItem.LHS.crntCursor.item
                withEach count in WHILE(nextItem.LHS.state != sSecondVisit and !(nextItem.LHS.crntCursor.item === crntInfon)):{
                    if(nextItem.LHS.fromDotted){
                        if(count==0 and crntInfon.value.items[crntIDX].itmMode==mDots ){
                            crntInfon.deleteNth(crntIDX)
                        }
                        nextItem.LHS.fromDotted <- false
                        nextItem.LHS.crntCursor.insertBeforeNth(crntIDX, nextItem.LHS.crntCursor.item)
                    }
                    nextItem <- stitch(nextItem, indent + "     ")
                    crntIDX <- crntIDX+1
                }
                retVal <- nextItem.successor
            }
            default: {print("Error in stitch(): invalid state\n")}
        }
        return(retVal)

    }

    our infDeepItr: tryMERGE(our aItem: agendaItem) <- {
        our infon: LHS <- agendaItem.LHS.crntCursor.item
        our infon: RHS <- agendaItem.RHS.crntCursor.item
        our infDeepItr: remainder <- NULL
        if(PropogateIndirectItem(agendaItem.LHS.crntCursor, agendaItem.RHS.crntCursor) > 0){return(NULL)}

        me bool: LooseType<-false   /- Strict or loose typing?
        me bool: reject <- false    /- ReturnStatus

        if(LHS.value.fType==tUnknown and RHS.value.fType!=tUnknown){
            agendaItem.copyRHSTypeToLHS <- 1
        } else {agendaItem.copyRHSTypeToLHS <- 0}

        me mergeCmd: sizeStatus; sizeStatus.cmd <- 0
        if(! LooseType){
            me int: LHSFormat <- LHS.size.format
            me int: RHSFormat <- RHS.size.format
            if(LHSFormat != fUnknown and RHSFormat != fUnknown){
                sizeStatus <- DO_VERIFY(LHS.size, RHS.size)
                if (sizeStatus.cmd==matchFailed){agendaItem.sizeAction<-sizeFailed; reject<-true;}
                else if (sizeStatus.cmd== wasVerified){agendaItem.sizeAction<-sizeVerified}
            }
            else if(LHSFormat == fUnknown and RHSFormat != fUnknown){agendaItem.sizeAction<-copySizeRHStoLHS}
            else if(LHSFormat != fUnknown and RHSFormat == fUnknown){agendaItem.sizeAction<-copySizeLHStoRHS}
            else if(LHSFormat == fUnknown and RHSFormat == fUnknown){CO_SUBSCRIBE_SIZES(LHS, RHS)}
        }
        me mergeCmd: valueStatus; valueStatus.cmd<-0
        if(!reject){
            me int: LHSFormat <- LHS.value.format
            me int: RHSFormat <- RHS.value.format

            if(LHSFormat != fUnknown and RHSFormat != fUnknown){
                valueStatus <- DO_VERIFY(LHS.value, RHS.value)
                if(valueStatus.cmd==matchFailed){agendaItem.valueAction<-valueFailed; reject<-true;}
                else if (valueStatus.cmd== wasVerified){agendaItem.valueAction<-valueVerified}
            }
            else if(LHSFormat == fUnknown and RHSFormat != fUnknown){agendaItem.valueAction<-copyValueRHStoLHS}
            else if(LHSFormat != fUnknown and RHSFormat == fUnknown){agendaItem.valueAction<-copyValueLHStoRHS}
            else if(LHSFormat == fUnknown and RHSFormat == fUnknown){CO_SUBSCRIBE_VALUES(LHS, RHS)}
        }

        if (valueStatus.cmd == wasVerified and RHS.value.fType == STR){
            me int: sizeDiff <- valueStatus.sizeDiff
            me int: rSize <- RHS.value.str.size()
            if(sizeDiff > 0 ){
                me string: remainderStr <- RHS.value.str.subStr(rSize -sizeDiff, sizeDiff)
                our infon: remainderInfon; Allocate(remainderInfon,remainderStr)
                Allocate(remainder)
                agendaItem.RHS.copyThisTo(remainder)
                Allocate(remainder.crntCursor)
                remainder.crntCursor.item <- remainderInfon

            }/- TODO: finish case where remainder is on the left

        }

        if(reject){agendaItem.rejected <- true; remainder <- NULL}

        return(remainder)
    }

    me int: addWorkItemsToAgenda(me int: priority, our aItem: pred, our infDeepItr: itrToProcess)<-{
        me int: numItemsAdded <- 0
        our infon: i <- itrToProcess.crntCursor.item
        if(i.wrkItmsAdded){return(0)}else{i.wrkItmsAdded <- true}
       /- theClip.dispAndWait(i, "addWorkItemsToAgenda - A")
        if(i.wrkList.size()==0){
            if(pred == NULL){   /- There is no RHS so make RHS NULL
                our infDeepItr: newIDP
                Allocate(newIDP)
                newIDP.crntCursor <- NULL
                our aItem: newItem
                Allocate(newItem)
                newItem.init(0,itrToProcess,newIDP)
                if(pred != NULL){newItem.predecessors.pushLast(pred)}
                agenda.addItem(newItem)
/- VIEWER     if(pred==NULL){topAItem <- newItem}
            }
        } else {
            withEach workItem in i.wrkList:{
                our infDeepItr: newIDP
                Allocate(newIDP)
                newIDP.initialize(workItem)
                our aItem: newItem
                Allocate(newItem)
                newItem.init(0,itrToProcess,newIDP)
                if(pred != NULL){newItem.predecessors.pushLast(pred)}
                agenda.addItem(newItem)
/- VIEWER   if(pred==NULL){topAItem <- newItem}
                workItem.wasProcessed <- true
                numItemsAdded <- numItemsAdded+1
            }
        }
        return(numItemsAdded)
    }

    void: normalize(our infon: i) <- {
      /-  testIterator(i);  exit(2)
       /- print("NORMALIZING: ", printInfon(i) ,"\n")
        our infDeepItr: ii
        Allocate(ii)
        ii.initialize(i)
        agenda.parentAgent <- self
        prepWorkList(ii)
        addWorkItemsToAgenda(0, NULL, ii)
        agenda.processAgenda()
        i.isNormed <- true
    }

    me string: fetchAsStr(their infon: i) <- {return(i.value.str)}
    me uint64: fetchAsNum(their infon: i) <- {return(i.value.num)}
    our infon: fetchAsList(their infon: i, me int: idx) <- {return(i.value.items[idx])}

    void: appendToWorld(our infon: newInfon) <- {
        our infon: dotsAtTheEnd <- world.value.items.last()
        world.value.items.popLast()
        world.value.items.pushLast(newInfon)
        world.value.items.pushLast(dotsAtTheEnd)
    }

    me bool: loadAndParseData() <- {
        me string: data <- readFileAsString("data.pr")
        if(data==""){
            print("File Error!\n\n")
            exit(2)
        }

        me EParser: parser
        parser.populateGrammar()
        parser.initPosStateSets(parser.infon_str, data)

        parser.doParse()
        if(parser.doesParseHaveError()){
            print("\nParse Error:", parser.errorMesg, " at line ", parser.errLineNum,":", parser.errCharPos, "\n")
            exit(2)
        } else {print("\nParse Succeeded\n")}

        our stateRec: topItem <- parser.resolve(parser.lastTopLevelItem, "")
        /-    parser.displayParse(topItem, "")
        Allocate(world)
        parser.Extract_infon_to_infon(topItem, world)
        /-    trackerMgr.allData.topInfon <- settingsAndData

        return(true)
    }

    me string: printPureInfon(their pureInfon: pure, their pureInfon: size) <- {
        me string: S <- ""
        me bool: showSize <- (size!=NULL and (pure.sizeGiven !=0) and (pure.fType!=LST or pure.format!=fLiteral))
        if(showSize){S <- S+printPureInfon(size, NULL)}
        if(pure.inverted){
            if(size!=NULL){S <- S+"-"}
            else if(pure.isLstSize==0){S <- S+"/"}
        } else{
                if(showSize){S <- S+"+"}
                else if(size==NULL and pure.isLstSize==0){S <- S+"*"}
        }
        if(pure.fType == NUM){
            if(pure.format == fUnknown){S<-S+"_"}
            else{
                me uint64:N <- pure.num
                S <- S+ toString(N)
            }
        } else if(pure.fType == STR){
            if(pure.format == fUnknown){S<-S+"$"}
            else{
                me string: str <- pure.str
                S <- S+"'"+ str + "'"
            }
        } else if(pure.fType == LST){
            me bool: showBar<-false
            me string: endBrkt<-"}"
            if(pure.cursor == cGetLast)     {S<-S+"["  endBrkt<-"]"}
            else if(pure.format == fConcat) {S<-S+"("  endBrkt<-")"}
            else {S <- S+("{")}
            if(pure.timelike){S <- S+"T "}
            if(pure.size!=NULL) {pure.size.isLstSize <- true}
            if(pure.size!=NULL and pure.format==fLiteral){S <- S+"~"+printPureInfon(pure.size, NULL) showBar<-true}
            if(pure.listSpec){
                if(showBar){S <- S+" "}
                S <- S + printInfon(pure.listSpec)
                showBar<-true
            }
            if(showBar){S <- S+"| "}
            withEach item in pure.items:{
                if(item_key>0){S <- S+" "}
                S <- S+printInfon(item)
            }
            S <- S+endBrkt
        } else {print("\nError: Unknown fType in printPureInfon(): ", pure.fType,"\n") exit(2)}
        return(S)
    }

    me string: printInfon(our infon: i) <- {
        me string: S <- ""
        if(i ==  NULL){return("NULL")}
        if(i.itmMode==mSubItems){S<-S+"#"}
        else if(i.itmMode==mDots){S<-S+"..."; return(S)}
        else if(i.itmMode==mTagDefn){S<-S+"class "+i.tag+" "}

        if(i.asNot){S <- S+"!"}
        if(i.infMode==isUnknown){S <- S+"?"}
        else if(i.infMode==newCursor){
            S <- S+"%"
            switch(i.cursorMode){
                case cThis:   {S <- S+"this"}
                case cWorld:  {S <- S+"W"}
                case cContext:{S <- S+"C"}
                case cPrev:   {S <- S+"prev"}
                case cNext:   {S <- S+"next"}
            }
        }
        else if(i.infMode==isTag){S <- S+i.tag}
        else if(i.infMode==isLiteral){
             S <- S+printPureInfon(i.value, i.size)
        } else {print("Error In PrintInfon()\n") exit(2)}

        if(i.isNormed ==0){
            withEach WorkItm in i.wrkList:{
                if(!WorkItm.wasProcessed){
                    S <- S+" = "+printInfon(WorkItm.item)
                }
            }

            if (i.isFunc){
                if(i.invertFunc){S <- S+"<!" + printInfon(i.aux1)}
                else{S <- S+"<:" + printInfon(i.aux1)}
            } else if(i.isFuncArg !=0){
                if(i.invertFunc){S <- S+"!>" + printInfon(i.aux1)}
                else{S <- S+":>" + printInfon(i.aux1)}
            }
        }
        return(S)
    }

    void: init(our infon: World) <- {
        world <- NULL
        context <- NULL
        if(World.value.fType!=LST or World.value.items.size()==0){ print("World is not valid for agant.\n"); return()}  /- TODO: Log error messages
        if(World.value.items.last().itmMode != mDots){ print("World must end with '...' for agant.\n"); return()}
        world <- World
        Allocate(context)
    }
}
struct ProteusCore{
    const string: worldFileName <- "WORLD.pr"
    me string: resourceDirectory
    our infon: WORLD
    our agent: a
    me EParser: parser

    our infon: parseAndExtract(me string: textToParse) <- {
        parser.initPosStateSets(parser.infon_str, textToParse)
        parser.doParse()
        if(parser.doesParseHaveError()){return(NULL)}
        else {
            our stateRec: parseData <- parser.resolve(parser.lastTopLevelItem, "")
            our infon: crntInfon
            Allocate(crntInfon)
            parser.Extract_infon_to_infon(parseData, crntInfon)
            return(crntInfon)
        }
    }
    me bool: parseToWorld(me string: proteusTxt) <- {}
    me void: loadWorld(me string: filename) <- {
        me string: worldFilePath <- resourceDirectory+'/'+filename
        me string: worldStr <- readFileAsString(worldFilePath)
        if(worldStr==""){worldStr <- "{...}"}
        WORLD <- parseAndExtract(worldStr)
        Allocate(a, WORLD)
    }
    me bool: init(me string: resourceDir) <- {
        resourceDirectory <- resourceDir
        parser.populateGrammar()
        loadWorld(worldFileName)
    }
    me void: deInit() <- {}
    /- hard-coded function interface
}

/-///////////////////////////////////////
do codeDataDisplay(infon, TAG_infonDumpMode)
